% Created 2018-09-09 Sun 17:16
% Intended LaTeX compiler: pdflatex
\documentclass[11pt]{article}
\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage{graphicx}
\usepackage{grffile}
\usepackage{longtable}
\usepackage{wrapfig}
\usepackage{rotating}
\usepackage[normalem]{ulem}
\usepackage{amsmath}
\usepackage{textcomp}
\usepackage{amssymb}
\usepackage{capt-of}
\usepackage{hyperref}
\usepackage{alphabeta} \usepackage[outputdir=..]{minted}
\author{Jack Henahan}
\date{\today}
\title{The Heretic's Emacs config}
\hypersetup{
 pdfauthor={Jack Henahan},
 pdftitle={The Heretic's Emacs config},
 pdfkeywords={},
 pdfsubject={},
 pdfcreator={Emacs 26.1 (Org mode 9.1.14)},
 pdflang={English}}
\begin{document}

\maketitle
\tableofcontents


\section{Basics}
\label{sec:orge91006c}
\subsection{Modern Libraries}
\label{sec:org46d31ff}
\subsubsection{Libraries}
\label{sec:orgd616988}
\begin{enumerate}
\item Async
\label{sec:orgd7d711c}
The \texttt{async} library makes writing asynchronous functions easier (since
async in Emacs is kind of a nightmare, otherwise).

\begin{minted}[]{common-lisp}
(use-package async
  :commands (async-start))
\end{minted}

\item \texttt{cl-lib}
\label{sec:orgc64bc18}
This library extends the "Lispiness" of Elisp, making it more like
Common Lisp though I always feel a bit dirty about using it.

\begin{minted}[]{common-lisp}
(use-package cl-lib
  :demand t)
\end{minted}

\item Lists
\label{sec:org7c655ac}
The list API in Elisp basically sucks, so there's \texttt{dash}. I'm
attempting to learn how to use \texttt{seq} properly, though, since it's
built in and should be just about at parity with \texttt{dash}'s API, if a
bit more verbose.

\begin{minted}[]{common-lisp}
(use-package dash
  :demand t)
\end{minted}

\item Files
\label{sec:org4d61c9b}
The file API in Elisp \textbf{also} sucks, so we have \texttt{f.el}.

\begin{minted}[]{common-lisp}
(use-package f
  :demand t)
\end{minted}

\item Strings
\label{sec:orgfc8a64b}
Keeping up the pattern, the string API in Elisp is hot garbage, so we
have \texttt{s.el} to make things nice.

\begin{minted}[]{common-lisp}
(use-package s
  :demand t)
\end{minted}

\item Hash Tables
\label{sec:orgda86af2}
Elisp's hash table API is based around \texttt{make-hash-table}, which is a
pretty awful function with five keyword arguments. This might almost
be worth it for how much faster hash tables are than alists, but
luckily \texttt{ht.el} means we don't have to choose.

\begin{minted}[]{common-lisp}
(use-package ht
  :demand t)
\end{minted}

\item \texttt{subr} extensions
\label{sec:org2a1179b}
\texttt{subr-x} provides some built-in equivalents to some of the modern APIs
above, some implemented in C.

\begin{minted}[]{common-lisp}
(require 'subr-x)
\end{minted}
\end{enumerate}
\subsection{Sensible Defaults}
\label{sec:org643db36}
\subsubsection{Default Directory}
\label{sec:orgc94ff45}
When using \texttt{find-file}, search from the user's home directory.
\begin{minted}[]{common-lisp}
(setq default-directory "~/")
\end{minted}

\subsubsection{Treat Camel-Case Words as separate words}
\label{sec:org720ff75}
\begin{minted}[]{common-lisp}
(add-hook 'prog-mode-hook 'subword-mode)
\end{minted}

\subsubsection{Increase GC threshold}
\label{sec:orgaebcd05}
Allow 20MB of memory (instead of 0.76MB) before calling garbage
collection. This means GC runs less often, which speeds up some
operations.
\begin{minted}[]{common-lisp}
(setq gc-cons-threshold 20000000)
\end{minted}

\subsubsection{Make Scripts Executable By Default}
\label{sec:orgf80cc60}
If your file starts with a shebang, the file will be marked executable
on save.
\begin{minted}[]{common-lisp}
(add-hook 'after-save-hook
          'executable-make-buffer-file-executable-if-script-p)
\end{minted}

\subsubsection{Transient Mark Mode}
\label{sec:orgcd2db10}
Transient mark means region highlighting works the way you would
expect it to coming from other editors.
\begin{minted}[]{common-lisp}
(transient-mark-mode t)
\end{minted}

\subsubsection{Short Confirmations}
\label{sec:orgb61741c}
Typing out \texttt{yes} and \texttt{no} is irritating. Just use \texttt{y} or \texttt{n}.
\begin{minted}[]{common-lisp}
(fset #'yes-or-no-p #'y-or-n-p)
\end{minted}

\subsubsection{macOS settings}
\label{sec:org7c94afa}
If you set Emacs as the default file handler for certain types of
files, double-clicking will open an entire new Emacs frame. This
setting causes Emacs to reuse the existing one.
\begin{minted}[]{common-lisp}
(the-with-operating-system macOS
  (setq ns-pop-up-frames nil))
\end{minted}
\subsection{Enable Disabled Commands}
\label{sec:orgfb3e1f5}
It is obvious to anyone that if a function is disabled then it must
be powerful, or at least interesting. I want them.

\subsubsection{Do it}
\label{sec:org3b8d008}
\begin{minted}[]{common-lisp}
(setq disabled-command-function nil)
\end{minted}
\subsection{Killing and Yanking (copying and pasting)}
\label{sec:org673bee9}
\subsubsection{Settings}
\label{sec:org6a39641}
\begin{enumerate}
\item Delete Selection
\label{sec:org71e8f18}
If you start typing when you have something selected, then the
selection will be deleted. If you press DEL while you have something
selected, it will be deleted rather than killed. (Otherwise, in both
cases the selection is deselected and the normal function of the key
is performed.)

\begin{minted}[]{common-lisp}
(delete-selection-mode 1)
\end{minted}

\begin{enumerate}
\item AUCTeX compatibility
\label{sec:org4619f12}
Make delete-selection-mode work properly with AUCTeX.
\begin{minted}[]{common-lisp}
(with-eval-after-load 'latex
  (put 'LaTeX-insert-left-brace 'delete-selection t))
\end{minted}
\end{enumerate}
\item Eliminate duplicates in kill ring
\label{sec:org760d6ea}
If you kill the same thing twice, you won't have to use \texttt{yank} twice
to get past it to older entries in the kill ring.
\begin{minted}[]{common-lisp}
(setq kill-do-not-save-duplicates t)
\end{minted}
\end{enumerate}
\subsection{Startup}
\label{sec:orgdec5a85}
By default, Emacs fills your universe with (FSF-approved and
GPL-compliant) garbage. I don't want a bit of it.
\subsubsection{Disable Startup Message}
\label{sec:orgab68e45}
I like GNU. You maybe like GNU. You're using Emacs. Whatever. You
don't need the "For information about Emacs\ldots{}" message.

\begin{minted}[]{common-lisp}
(defalias 'the--advice-inhibit-startup-echo-area-message #'ignore
  "Unconditionally inhibit the startup message in the echo area.
This is an `:override' advice for
`display-startup-echo-area-message'.")

(advice-add #'display-startup-echo-area-message :override
            #'the--advice-inhibit-startup-echo-area-message)
\end{minted}

\subsubsection{Disable About}
\label{sec:orgb3f6d73}
If I wanted to know about GNU Emacs, there are dozens of doc
options. I do not need that buffer on startup. We disable that and
instead use our own fancy dashboard.

\begin{minted}[]{common-lisp}
(use-package dashboard
  :demand t
  :after (org-agenda projectile)
  :config
  (setq recentf-exclude (-map 'f-canonical (org-agenda-files)))
  (setq dashboard-banner-logo-title "REPENT!")
  (setq dashboard-startup-banner (f-expand "heresy.png" the-image-directory))
  (if (f-exists? (f-expand "straight/build-cache.el" user-emacs-directory))
      (setq dashboard-items '((recents . 5)
                              (bookmarks . 5)
                              (projects . 5)
                              (agenda . 5)
                              (registers . 5)))
    (setq dashboard-items '((recents . 5)
                            (bookmarks . 5)
                            (registers . 5))))
  (dashboard-setup-startup-hook))
\end{minted}

\subsubsection{Blank Scratch Buffer}
\label{sec:orgaca407a}
I know what a scratch buffer is. Hush.
\begin{minted}[]{common-lisp}
(setq initial-scratch-message nil)
\end{minted}

\subsubsection{Emacs Server}
\label{sec:org4ccbf40}
Start up an Emacs server process so we can attach \texttt{emacsclient} to it
and get that fast response time the Vim people are so smug about.

\begin{minted}[]{common-lisp}
(server-start)
\end{minted}
\subsection{Auto-Revert}
\label{sec:orgcee2881}
\subsubsection{Settings}
\label{sec:org2237989}
Turn the delay on auto-reloading from 5 seconds down to 1 second. We
have to do this before turning on \texttt{auto-revert-mode} for the change to
take effect, unless we do it through \texttt{customize-set-variable} (which
is slow enough to show up in startup profiling).

\begin{minted}[]{common-lisp}
(setq auto-revert-interval 1)
\end{minted}

Automatically reload files that were changed on disk, if they have not
been modified in Emacs since the last time they were saved.

\begin{minted}[]{common-lisp}
(global-auto-revert-mode 1)
\end{minted}

Only automatically revert buffers that are visible. This should
improve performance (because if you have 200 buffers open\ldots{}). This
code is originally based on this \href{http://emacs.stackexchange.com/a/28899/12534}{Emacs.SE thread}.

Note that calling \texttt{global-auto-revert-mode} above triggers an
autoload for \texttt{autorevert}, so there's no need to `require' it again
here.

\begin{minted}[]{common-lisp}
(el-patch-defun auto-revert-buffers ()
  "Revert buffers as specified by Auto-Revert and Global Auto-Revert Mode.
Should `global-auto-revert-mode' be active all file buffers are checked.
Should `auto-revert-mode' be active in some buffers, those buffers
are checked.
Non-file buffers that have a custom `revert-buffer-function' and
`buffer-stale-function' are reverted either when Auto-Revert
Mode is active in that buffer, or when the variable
`global-auto-revert-non-file-buffers' is non-nil and Global
Auto-Revert Mode is active.
This function stops whenever there is user input.  The buffers not
checked are stored in the variable `auto-revert-remaining-buffers'.
To avoid starvation, the buffers in `auto-revert-remaining-buffers'
are checked first the next time this function is called.
This function is also responsible for removing buffers no longer in
Auto-Revert mode from `auto-revert-buffer-list', and for canceling
the timer when no buffers need to be checked."

  (setq auto-revert-buffers-counter
        (1+ auto-revert-buffers-counter))

  (save-match-data
    (let ((bufs (el-patch-wrap 2
                  (cl-remove-if-not
                   #'get-buffer-window
                   (if global-auto-revert-mode
                       (buffer-list)
                     auto-revert-buffer-list))))
          remaining new)
      ;; Partition `bufs' into two halves depending on whether or not
      ;; the buffers are in `auto-revert-remaining-buffers'.  The two
      ;; halves are then re-joined with the "remaining" buffers at the
      ;; head of the list.
      (dolist (buf auto-revert-remaining-buffers)
        (if (memq buf bufs)
            (push buf remaining)))
      (dolist (buf bufs)
        (if (not (memq buf remaining))
            (push buf new)))
      (setq bufs (nreverse (nconc new remaining)))
      (while (and bufs
                  (not (and auto-revert-stop-on-user-input
                            (input-pending-p))))
        (let ((buf (car bufs)))
          (if (buffer-live-p buf)
              (with-current-buffer buf
                ;; Test if someone has turned off Auto-Revert Mode in a
                ;; non-standard way, for example by changing major mode.
                (if (and (not auto-revert-mode)
                         (not auto-revert-tail-mode)
                         (memq buf auto-revert-buffer-list))
                    (setq auto-revert-buffer-list
                          (delq buf auto-revert-buffer-list)))
                (when (auto-revert-active-p)
                  ;; Enable file notification.
                  (when (and auto-revert-use-notify
                             (not auto-revert-notify-watch-descriptor))
                    (auto-revert-notify-add-watch))
                  (auto-revert-handler)))
            ;; Remove dead buffer from `auto-revert-buffer-list'.
            (setq auto-revert-buffer-list
                  (delq buf auto-revert-buffer-list))))
        (setq bufs (cdr bufs)))
      (setq auto-revert-remaining-buffers bufs)
      ;; Check if we should cancel the timer.
      (when (and (not global-auto-revert-mode)
                 (null auto-revert-buffer-list))
        (cancel-timer auto-revert-timer)
        (setq auto-revert-timer nil)))))
\end{minted}

Auto-revert all buffers, not only file-visiting buffers. The docstring
warns about potential performance problems but this should not be an
issue since we only revert visible buffers.

\begin{minted}[]{common-lisp}
(setq global-auto-revert-non-file-buffers t)
\end{minted}

Since we automatically revert all visible buffers after one second,
there's no point in asking the user whether or not they want to do it
when they find a file. This disables that prompt.

\begin{minted}[]{common-lisp}
(setq revert-without-query '(".*"))
\end{minted}

Prevent \textbf{Help} buffers from asking for confirmation about reverting.

\begin{minted}[]{common-lisp}
(with-eval-after-load 'help-mode
  (defun the--advice-disable-help-mode-revert-prompt
      (help-mode-revert-buffer _ignore-auto _noconfirm)
    (funcall help-mode-revert-buffer _ignore-auto 'noconfirm))
  (advice-add #'help-mode-revert-buffer :around
              #'the--advice-disable-help-mode-revert-prompt))
\end{minted}

Don't show it in the mode line.

\begin{minted}[]{common-lisp}
(setq auto-revert-mode-text nil)
\end{minted}
\subsection{Saving Files}
\label{sec:org90623e8}
\section{Package Management}
\label{sec:orga91817a}
\subsection{Package Management}
\label{sec:orgdf7a07d}
\subsubsection{Disable \texttt{package.el}}
\label{sec:orga6de3c9}
We use \texttt{straight.el} in this household, and like it! Emacs will
initialize \texttt{package.el} unless we tell it not to, so we do so.

\begin{minted}[]{common-lisp}
(setq package-enable-at-startup nil)
\end{minted}

\subsubsection{Bootstrap \texttt{straight.el}}
\label{sec:org4a146e0}
We are using a package manager called straight.el. This code, which is
taken from \href{https://github.com/raxod502/straight.el}{the README}, bootstraps the system (because obviously the
package manager is unable to install and load itself, if it is not
already installed and loaded).

\begin{minted}[]{common-lisp}
(let ((bootstrap-file (concat user-emacs-directory "straight/repos/straight.el/bootstrap.el"))
      (bootstrap-version 3))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))
\end{minted}

\subsubsection{\texttt{use-package}}
\label{sec:org1aade48}
To handle a lot of useful tasks related to package configuration, we
use a library called `use-package', which provides a macro by the same
name. This macro automates many common tasks, like autoloading
functions, binding keys, registering major modes, and lazy-loading,
through the use of keyword arguments. See the README.

\begin{minted}[]{common-lisp}
(straight-use-package 'use-package)
\end{minted}

\begin{enumerate}
\item \texttt{straight.el} integration
\label{sec:orge3d2dca}
Tell use-package to automatically install packages if they are
missing. By default, packages are installed via \href{https://github.com/raxod502/straight.el}{\texttt{straight.el}}, which
draws package installation recipes (short lists explaining where to
download the package) from \href{http://melpa.org/\#/}{MELPA}, \href{https://elpa.gnu.org/}{GNU ELPA}, and \href{https://emacsmirror.net/}{EmacsMirror}. (But you
can also specify a recipe manually by putting \texttt{:straight} in the
\texttt{use-package} call, which is an extension to \texttt{use-package} provided by
\texttt{straight.el}.) Learn more about recipe formatting from the \href{https://github.com/melpa/melpa\#recipe-format}{MELPA
README}.

\begin{minted}[]{common-lisp}
(setq straight-use-package-by-default t)
\end{minted}

\item Lazy-loading
\label{sec:org27ab7d0}
Tell use-package to always load packages lazily unless told otherwise.
It's nicer to have this kind of thing be deterministic: if `:demand'
is present, the loading is eager; otherwise, the loading is lazy. See
\href{https://github.com/jwiegley/use-package\#notes-about-lazy-loading}{the \texttt{use-package} documentation}.

\begin{minted}[]{common-lisp}
(setq use-package-always-defer t)
(setq use-package-verbose t)
\end{minted}
\end{enumerate}
\subsection{Future-proof patches}
\label{sec:org1d65ef9}
\subsubsection{El-Patch}
\label{sec:orgebab702}
Occasionally you need to customize a small part of a large function
defined by another package. This library provides an elegant,
clear, and robust way of doing so. See \href{https://github.com/raxod502/el-patch}{the README}.

\begin{minted}[]{common-lisp}
(use-package el-patch
  :straight (:host github
           :repo "raxod502/el-patch"
           :branch "develop")
  :config

  ;; When patching variable definitions, override the original values.
  (setq el-patch-use-aggressive-defvar t)

  ;; Support for deferred installation in `el-patch-validate-all'.

  (defun the-require-with-deferred-install (feature &rest args)
    "Require FEATURE, installing PACKAGE if necessary.
\(fn FEATURE &optional PACKAGE)"
    (let ((package feature))
      (when args
        (setq package (car args)))
      (when package
        (use-package-install-deferred-package package :el-patch))
      (require feature)))

  (setq el-patch-require-function #'the-require-with-deferred-install))
\end{minted}
\section{Customization}
\label{sec:org3df7149}
\subsection{Customization Group}
\label{sec:org7b60f6c}
\subsubsection{THE group}
\label{sec:orgea5a0e7}
Here we define a customization group for THE. This allows users
to customize the variables declared here in a user-friendly way
using the Custom interface.

\begin{minted}[]{common-lisp}
(defgroup the nil
  "Customize your THE experience"
  :group 'emacs)
\end{minted}
\section{System Integration}
\label{sec:org36521ff}
\subsection{OS Integration}
\label{sec:orgba3067d}
\subsubsection{OS-specific Customization}
\label{sec:org847f491}
\begin{enumerate}
\item OS detection
\label{sec:org204dfdb}
Ideally, we detect the operating system here, as well as giving the
option to customize it directly in case we get it wrong.
\begin{minted}[]{common-lisp}
(defcustom the-operating-system
  (pcase system-type
    ('darwin 'macOS)
    ((or 'ms-dos 'windows-nt 'cygwin) 'windows)
    (_ 'linux))
  "Specifies the operating system.
This can be `macOS', `linux', or `windows'. Normally this is
automatically detected and does not need to be changed."
  :group 'the
  :type '(choice (const :tag "macOS" macOS)
                 (const :tag "Windows" windows)
                 (const :tag "Linux" linux)))
\end{minted}

\item OS-specific settings
\label{sec:org3a1679c}
This macro allows us to handle things like operating system specific
clipboard/mouse hacks and other things like that.

\begin{minted}[]{common-lisp}
(defmacro the-with-operating-system (os &rest body)
  "If the operating system is OS, eval BODY.
See `the-operating-system' for the possible values of OS,
which should not be quoted."
  (declare (indent 1))
  `(when (eq the-operating-system ',os)
     ,@body))
\end{minted}
\end{enumerate}
\subsubsection{Path Settings}
\label{sec:org9320306}
\begin{enumerate}
\item Path Fixes
\label{sec:org169c73e}
In the terminal, the mouse and clipboard don't work properly. But in
windowed Emacsen, the \texttt{PATH} is not necessarily set correctly! You
can't win.

\begin{minted}[]{common-lisp}
(use-package exec-path-from-shell
  :demand t
  :config
  (the-with-operating-system macOS
    (exec-path-from-shell-initialize)
    (exec-path-from-shell-copy-env "RUST_SRC_PATH"))
  (the-with-operating-system linux
    (exec-path-from-shell-initialize)))
\end{minted}
\end{enumerate}
\subsubsection{Clipboard Integration}
\label{sec:orge282e5e}
\begin{enumerate}
\item macOS integration
\label{sec:org9f46d3a}
Like mouse integration, clipboard integration
works properly in windowed Emacs but not in terminal Emacs (at
least by default). This code was originally based on \href{https://gist.github.com/the-kenny/267162}{1}, and then
modified based on \href{http://emacs.stackexchange.com/q/26471/12534}{2}.

\begin{minted}[]{common-lisp}
(the-with-operating-system macOS
  (use-package osx-clipboard
    :demand t
    :diminish
    :config
    (osx-clipboard-mode +1)))
\end{minted}

\item Inter-program paste
\label{sec:org905ca5d}
If you have something on the system clipboard, and then kill something
in Emacs, then by default whatever you had on the system clipboard is
gone and there is no way to get it back. Setting the following option
makes it so that when you kill something in Emacs, whatever was
previously on the system clipboard is pushed into the kill ring. This
way, you can paste it with \texttt{yank-pop}.
\begin{minted}[]{common-lisp}
(setq save-interprogram-paste-before-kill t)
\end{minted}
\end{enumerate}
\subsection{UI Integration}
\label{sec:org505d5fa}
\subsubsection{Window System}
\label{sec:org87720eb}
These macros give us a convenient way to conditionally execute code
based on the active window system.

\begin{enumerate}
\item Macros
\label{sec:orgadb2c76}
\begin{minted}[]{common-lisp}
(defmacro the-with-windowed-emacs (&rest body)
  "Eval BODY if Emacs is windowed, else return nil."
  (declare (indent defun))
  `(when (display-graphic-p)
     ,@body))

(defmacro the-with-terminal-emacs (&rest body)
  "Eval BODY if Emacs is not windowed, else return nil."
  (declare (indent defun))
  `(unless (display-graphic-p)
     ,@body))
\end{minted}
\end{enumerate}
\section{Config Management}
\label{sec:orgb275226}
\subsection{Config File Utilities}
\label{sec:org39235de}
\subsubsection{Apache configs}
\label{sec:org986b4b4}
\begin{minted}[]{common-lisp}
(use-package apache-mode)
\end{minted}
\subsubsection{Dockerfiles}
\label{sec:org46b7d70}
\begin{minted}[]{common-lisp}
(use-package dockerfile-mode)
\end{minted}

\subsubsection{Git files}
\label{sec:orgb21e5d4}
\begin{enumerate}
\item Git config and modules
\label{sec:orgd506add}
\begin{minted}[]{common-lisp}
(use-package gitconfig-mode)
\end{minted}

\item Git ignore files
\label{sec:org2f3fea2}
\begin{minted}[]{common-lisp}
(use-package gitignore-mode)
\end{minted}
\end{enumerate}

\subsubsection{SSH configs}
\label{sec:org8811996}
\begin{minted}[]{common-lisp}
(use-package ssh-config-mode)
\end{minted}

\subsubsection{YAML}
\label{sec:orgd2de083}
I edit a lot of YAML files, especially Ansible configs. The current
version of \texttt{emacs-ansible} has a hard dependency on \texttt{auto-complete},
which we don't use, so until there's a version without that
dependency, we just turn \texttt{yaml-mode} on whenever we think we're in an
Ansible file. \texttt{auto-fill} is also turned off in YAML buffers because
it breaks things.

\begin{minted}[]{common-lisp}
(use-package yaml-mode
  :init
  (setq the--ansible-filename-re ".*\\(main\.yml\\|site\.yml\\|encrypted\.yml\\|roles/.+\.yml\\|group_vars/.+\\|host_vars/.+\\)")
  (add-to-list 'auto-mode-alist `(,the--ansible-filename-re . yaml-mode))
  :config
  (defun the--disable-auto-fill-mode ()
    (auto-fill-mode -1))

  (add-hook 'yaml-mode-hook #'the--disable-auto-fill-mode))
\end{minted}

\subsubsection{Jinja2}
\label{sec:orgc7038b1}
Jinja2 is the template format of record for Ansible, so we just add
basic support here.
\begin{minted}[]{common-lisp}
(use-package jinja2-mode)
\end{minted}
\subsection{Emacs}
\label{sec:org0bae0e7}
\subsubsection{emacs.d Organization}
\label{sec:org6f0e682}
\begin{enumerate}
\item \texttt{no-littering}
\label{sec:orga43f69f}
A lot of packages (and also a lot of Emacs defaults) throw files all
over your config directory. \texttt{no-littering} sets a lot of sensible
defaults for commonly used packages to keep the config directory
manageable and discoverable.

\begin{minted}[]{common-lisp}
(use-package no-littering
  :demand t)
\end{minted}
\end{enumerate}
\section{Documentation}
\label{sec:org8915f29}
\subsection{Better Help}
\label{sec:orgac4673a}
\subsubsection{Helpful}
\label{sec:org89fb176}
We alias common help methods to their Helpful equivalents because
Helpful is a much nicer version of the built-in help.
\begin{minted}[]{common-lisp}
(use-package helpful
  :demand t
  :config
  (defalias #'describe-key #'helpful-key)
  (defalias #'describe-function #'helpful-callable)
  (defalias #'describe-variable #'helpful-variable)
  (defalias #'describe-symbol #'helpful-symbol))
\end{minted}
\subsection{ElDoc}
\label{sec:orgb1b2a91}
\subsubsection{Settings}
\label{sec:orgfd9f62b}
Show ElDoc messages in the echo area immediately, instead of after 1/2
a second.

\begin{minted}[]{common-lisp}
(setq eldoc-idle-delay 0)
\end{minted}

Always truncate ElDoc messages to one line. This prevents the echo
area from resizing itself unexpectedly when point is on a variable
with a multiline docstring.

\begin{minted}[]{common-lisp}
(setq eldoc-echo-area-use-multiline-p nil)
\end{minted}

Don't show ElDoc in the mode line.

\begin{minted}[]{common-lisp}
(setq eldoc-minor-mode-string nil)
\end{minted}

Slow down ElDoc if metadata fetching is causing performance issues.

\begin{minted}[]{common-lisp}
(defun the-eldoc-toggle-slow ()
  "Slow down `eldoc' by turning up the delay before metadata is shown.
This is done in `the-slow-autocomplete-mode'."
  (if the-slow-autocomplete-mode
      (setq-local eldoc-idle-delay 1)
    (kill-local-variable 'eldoc-idle-delay)))

(add-hook 'the-slow-autocomplete-mode-hook #'the-eldoc-toggle-slow)
\end{minted}
\section{Keybindings}
\label{sec:org15ee967}
\subsection{Binding Keys}
\label{sec:org6f142d5}
\subsubsection{Custom Prefix}
\label{sec:orgebe2af7}
There's a lot of room for keybindings, but we rely on a common prefix
for discoverability and to leave room for extension. This also makes
creating modal bindings later quite a bit easier.

\begin{minted}[]{common-lisp}
(defcustom the-prefix "M-T"
  "Prefix key sequence for The-related keybindings.
This is a string as would be passed to `kbd'."
  :group 'the
  :type 'string)
\end{minted}

For convenience, we also have a function that will create binding
strings using our prefix. This mainly gets used in bind-key
declarations until I can figure out how to evaluate code in org-table
cells to make the whole thing more customizable.

\begin{minted}[]{common-lisp}
(defun the-join-keys (&rest keys)
  "Join key sequences. Empty strings and nils are discarded.
\(the--join-keys \"M-P e\" \"e i\") => \"M-P e e i\"
\(the--join-keys \"M-P\" \"\" \"e i\") => \"M-P e i\""
  (string-join (remove "" (mapcar #'string-trim (remove nil keys))) " "))
\end{minted}

\subsubsection{\texttt{bind-key}}
\label{sec:org54e2ce7}
\texttt{bind-key} is the prettier cousin of \texttt{define-key} and
\texttt{global-set-key}, as well as providing the \texttt{:bind} family of keywords
in \texttt{use-package},

\begin{minted}[]{common-lisp}
(use-package bind-key)
\end{minted}
\subsection{Hydra}
\label{sec:orgf3e768e}
Hydras are a really fancy feature that let you create families of
related bindings with a common prefix.
\subsubsection{\texttt{use-package} declaration}
\label{sec:orgc942cdf}
\begin{minted}[]{common-lisp}
(use-package hydra
  :demand t)
\end{minted}
\section{UI}
\label{sec:orgdb8a438}
\subsection{Appearance}
\label{sec:org48e7165}
\subsubsection{Basic Setup}
\label{sec:orgeb19d9b}
This file has appearance tweaks that are unrelated to the color
theme. Menus, scroll bars, bells, cursors, and so on. See also
\texttt{the-theme}, which customizes the color theme specifically.

\subsubsection{Fullscreen}
\label{sec:org62eb808}
I use \texttt{chunkwm} to manage most windows, including Emacs, so the native
fullscreen mode is unnecessary. It's also necessary to set pixelwise
frame resizing non-nil for a variety of window managers. I don't see
any particular harm in having it on, regardless of WM.

\begin{minted}[]{common-lisp}
(the-with-operating-system macOS
  (setq ns-use-native-fullscreen nil))
(setq frame-resize-pixelwise t)
\end{minted}

\subsubsection{Interface Cleanup}
\label{sec:org02de84b}
Emacs defaults are a nightmare of toolbars and scrollbars and such
nonsense. We'll turn all of that off.

\begin{minted}[]{common-lisp}
(menu-bar-mode -1)
(setq ring-bell-function #'ignore)
(scroll-bar-mode -1)
(defun the-disable-scroll-bars (frame)
  (modify-frame-parameters frame
                           '((vertical-scroll-bars . nil)
                             (horizontal-scroll-bars . nil))))
(add-hook 'after-make-frame-functions 'the-disable-scroll-bars)
(tool-bar-mode -1)
(blink-cursor-mode -1)
\end{minted}

\subsubsection{Keystroke Display}
\label{sec:orge3fdc25}
Display keystrokes in the echo area immediately, not after one
second. We can't set the delay to zero because somebody thought it
would be a good idea to have that value suppress keystroke display
entirely.

\begin{minted}[]{common-lisp}
(setq echo-keystrokes 1e-6)
\end{minted}

\subsubsection{No Title Bars}
\label{sec:org6b2588b}
I put a lot of effort into purging title bars from most of the
software I use on a regular basis (what a waste of real estate), and
in Emacs 26 (might really be 26.2 or so) this is built in. For earlier
versions, patches exist to get the same effect.

\begin{minted}[]{common-lisp}
(if (version<= "26" emacs-version)
    (progn
      (setq frame-resize-pixelwise t)
      (setq default-frame-scroll-bars 'none)
      (add-to-list 'default-frame-alist '(ns-transparent-titlebar . t))
      (add-to-list 'default-frame-alist '(ns-appearance . dark))
                                        ;(setq default-frame-alist '((undecorated . t)))
      ))
\end{minted}

\subsubsection{Fonts}
\label{sec:org9369804}
I use Pragmata Pro everywhere, but I'll eventually figure out how to
deal with fonts properly and allow this to be specified.

\subsubsection{Adjust font size by screen resolution}
\label{sec:org2daedb1}
The biggest issue I have with multiple monitors is that the font size
is all over the place. The functions below just set up some reasonable
defaults and machinery to change the size depending on the resolution
of the monitor.

\begin{minted}[]{common-lisp}
(defun the-fontify-frame (frame)
  (interactive)
  (the-with-windowed-emacs
    (if (> (x-display-pixel-width) 2000)
        (set-frame-parameter frame 'font "PragmataPro 22") ;; Cinema Display
      (set-frame-parameter frame 'font "PragmataPro 16"))))

(defun the-fontify-this-frame ()
  (interactive)
  (the-fontify-frame nil))

(defun the-fontify-idle ()
  (interactive)
  (the-fontify-this-frame)
  (run-with-idle-timer 1 t 'the-fontify-this-frame))

(call-interactively 'the-fontify-idle)
\end{minted}

\subsubsection{Fancy nonsense}
\label{sec:orgd9c2127}
Transparency and whatnot.
\begin{minted}[]{common-lisp}
(defcustom the-fancy-stuff nil
  "Non-nil means turn on a bunch of weird appearance tweaks like
transparency."
  :group 'the
  :type 'boolean)

(if the-fancy-stuff
    (add-to-list 'default-frame-alist '(alpha . (85 . 50))))
\end{minted}
\subsection{Theme}
\label{sec:org6c588bb}
\subsubsection{Utilities}
\label{sec:org7a82235}

This function is useful for reformatting lispy names (like
\texttt{deeper-blue}) into user-friendly strings (like "Deeper Blue") for
the Custom interface.
\begin{minted}[]{common-lisp}
(defun the--unlispify (name)
  "Converts \"deep-blue\" to \"Deep Blue\"."
  (capitalize
   (replace-regexp-in-string
    "-" " " name)))
\end{minted}

This is a handy macro for conditionally enabling color theme
customizations.
\begin{minted}[]{common-lisp}
(defmacro the-with-color-theme (theme &rest body)
  "If the current color theme is THEME, eval BODY; else return nil.
The current color theme is determined by consulting
`the-color-theme'."
  (declare (indent 1))
  ;; `theme' should be a symbol so we can use `eq'.
  `(when (eq ',theme the-color-theme)
     ,@body))
\end{minted}

\subsubsection{Default Color Scheme}
\label{sec:org71874d7}

The default color scheme is Gruvbox, but you can set it to whatever
you like.

\begin{minted}[]{common-lisp}
(defcustom the-color-theme 'gruvbox
  "Specifies the color theme used by The.
You can use anything listed by `custom-available-themes'. If you
wish to use your own color theme, you can set this to nil."
  :group 'the
  :type `(choice ,@(mapcar (lambda (theme)
                             `(const :tag
                                     ,(if theme
                                          (the--unlispify
                                           (symbol-name theme))
                                        "None")
                                     ,theme))
                           (cons
                            nil
                            (sort
                             (append
                              (custom-available-themes)
                              '(leuven
                                gruvbox))
                             #'string-lessp)))))
\end{minted}

Defer color theme loading until after init, which helps to avoid
weirdness during the processing of the local init-file.

\begin{minted}[]{common-lisp}
(defcustom the-defer-color-theme t
  "Non-nil means defer loading the color theme until after init.
Otherwise, the color theme is loaded whenever `the-theme' is
loaded."
  :group 'the
  :type 'boolean)
\end{minted}

\subsubsection{Leuven Customization}
\label{sec:org6cda84b}
\begin{itemize}
\item Change the highlight color from yellow to blue
\item Don't underline current search match
\item Lighten the search highlight face and remove the underline
\item Don't underline mismatched parens

\begin{minted}[]{common-lisp}
(the-with-color-theme leuven
  (set-face-background 'highlight "#B1EAFC")
  (set-face-underline 'isearch nil)
  (set-face-background 'lazy-highlight "#B1EAFC")
  (set-face-underline 'lazy-highlight nil)
  (set-face-underline 'show-paren-mismatch nil))
\end{minted}
\end{itemize}

\subsubsection{Gruvbox installation}
\label{sec:org3b35143}
We register the Gruvbox package with Straight, but it is only
downloaded if the theme is active.
\begin{minted}[]{common-lisp}
(straight-register-package 'gruvbox-theme)
(the-with-color-theme gruvbox
  (use-package gruvbox-theme))
\end{minted}

\subsubsection{Actually load the theme}
\label{sec:org78211fe}
Load the appropriate color scheme as specified in
\texttt{the-color-theme}.
\begin{minted}[]{common-lisp}
(when the-color-theme
  (if the-defer-color-theme
      (progn
        (eval-and-compile
          (defun the-load-color-theme ()
            "Load the The color theme, as given by `the-color-theme'.
If there is an error, report it as a warning."
            (condition-case-unless-debug error-data
                (load-theme the-color-theme 'no-confirm)
              (error (warn "Could not load color theme: %s"
                           (error-message-string error-data))))))
        (add-hook 'after-init-hook #'the-load-color-theme))
    (load-theme the-color-theme 'no-confirm)))
\end{minted}
\subsection{Modeline Configuration}
\label{sec:orgb64f408}
\subsubsection{Diminish}
\label{sec:org715eaf5}
\texttt{diminish} allows us to change the display of minor modes in the
modeline. I prefer Delight, but \texttt{diminish} is the standard used by
many packages.
\begin{minted}[]{common-lisp}
(use-package diminish
  :demand t
  :config
  (with-eval-after-load 'subword
    (diminish 'subword-mode))
  (diminish 'filladapt-mode)
  (with-eval-after-load 'outline
    (diminish 'outline-minor-mode))
  (diminish 'smerge-mode)
  (diminish 'whitespace-mode)
  (diminish 'abbrev-mode)
  (diminish 'auto-fill-function)
  (with-eval-after-load 'page-break-lines
    (diminish 'page-break-lines-mode))
  (diminish 'visual-line-mode "ω")
  (diminish 'eldoc-mode "ε")
  )
\end{minted}
\subsubsection{Delight}
\label{sec:org1fd138d}
\texttt{delight} allows us to change the display of minor and major modes in
the modeline. Spaceline is gonna do a lot of this work for us, but for
anything it doesn't catch we'll make our own lighter. This also gives
us the \texttt{:delight} keyword in our \texttt{use-package} declarations.

\begin{minted}[]{common-lisp}
(use-package delight
  :demand t
  :config
  (delight
   '((emacs-lisp-mode "ξ" :major))))
\end{minted}
\subsubsection{Nyan!}
\label{sec:orgcd563ff}
Gotta have that cat! This will add a Nyan Cat progress indicator to
the modeline.
\begin{minted}[]{common-lisp}
(use-package nyan-mode
  :demand t
  :init
  (setq nyan-animate-nyancat t
        nyan-wavy-trail t)
  :config
  (nyan-mode 1))
\end{minted}
\subsubsection{{\bfseries\sffamily TODO} Custom Modeline}
\label{sec:org5bd5a61}
\begin{minted}[]{common-lisp}
(use-package spaceline
  :init
  (require 'spaceline-config)
  :config
  (spaceline-spacemacs-theme))
\end{minted}
\subsection{Emojis!}
\label{sec:org7e45c4e}
\subsubsection{\texttt{emojify}}
\label{sec:orgbf5582d}
Emojify renders a variety of strings as emojis, as well as providing
some nice interactive functions to get emojis all over the place.

\begin{minted}[]{common-lisp}
(use-package emojify
  :init
  (add-hook 'after-init-hook #'global-emojify-mode))
\end{minted}
\subsection{Fancy Pragmata Pro ligatures}
\label{sec:org1fad614}
\begin{minted}[]{common-lisp}
;; -*- lexical-binding: t; -*-
;;; the-pragmata.el --- ligatures for Pragmata Pro

(setq prettify-symbols-unprettify-at-point 'right-edge)

(defconst pragmatapro-prettify-symbols-alist
  (mapcar (lambda (s)
            `(,(car s)
              .
              ,(vconcat
                (apply 'vconcat
                       (make-list
                        (- (length (car s)) 1)
                        (vector (decode-char 'ucs #X0020) '(Br . Bl))))
                (vector (decode-char 'ucs (cadr s))))))
          '(("[ERROR]"   #XE380)
            ("[DEBUG]"   #XE381)
            ("[INFO]"    #XE382)
            ("[WARN]"    #XE383)
            ("[WARNING]" #XE384)
            ("[ERR]"     #XE385)
            ("[FATAL]"   #XE386)
            ("[TRACE]"   #XE387)
            ("[FIXME]"   #XE388)
            ("[TODO]"    #XE389)
            ("[BUG]"     #XE38A)
            ("[NOTE]"    #XE38B)
            ("[HACK]"    #XE38C)
            ("[MARK]"    #XE38D)
            ("!!"        #XE900)
            ("!="        #XE901)
            ("!=="       #XE902)
            ("!!!"       #XE903)
            ("!≡"        #XE904)
            ("!≡≡"       #XE905)
            ("!>"        #XE906)
            ("!=<"       #XE907)
            ("#("        #XE920)
            ("#_"        #XE921)
            ("#{"        #XE922)
            ("#?"        #XE923)
            ("#>"        #XE924)
            ("##"        #XE925)
            ("#_("       #XE926)
            ("%="        #XE930)
            ("%>"        #XE931)
            ("%>%"       #XE932)
            ("%<%"       #XE933)
            ("&%"        #XE940)
            ("&&"        #XE941)
            ("&*"        #XE942)
            ("&+"        #XE943)
            ("&-"        #XE944)
            ("&/"        #XE945)
            ("&="        #XE946)
            ("&&&"       #XE947)
            ("&>"        #XE948)
            ("$>"        #XE955)
            ("***"       #XE960)
            ("*="        #XE961)
            ("*/"        #XE962)
            ("*>"        #XE963)
            ("++"        #XE970)
            ("+++"       #XE971)
            ("+="        #XE972)
            ("+>"        #XE973)
            ("++="       #XE974)
            ("--"        #XE980)
            ("-<"        #XE981)
            ("-<<"       #XE982)
            ("-="        #XE983)
            ("->"        #XE984)
            ("->>"       #XE985)
            ("---"       #XE986)
            ("-->"       #XE987)
            ("-+-"       #XE988)
            ("-\\/"      #XE989)
            ("-|>"       #XE98A)
            ("-<|"       #XE98B)
            (".."        #XE990)
            ("..."       #XE991)
            ("..<"       #XE992)
            (".>"        #XE993)
            (".~"        #XE994)
            (".="        #XE995)
            ("/*"        #XE9A0)
            ("//"        #XE9A1)
            ("/>"        #XE9A2)
            ("/="        #XE9A3)
            ("/=="       #XE9A4)
            ("///"       #XE9A5)
            ("/**"       #XE9A6)
            (":::"       #XE9AF)
            ("::"        #XE9B0)
            (":="        #XE9B1)
            (":≡"        #XE9B2)
            (":>"        #XE9B3)
            (":=>"       #XE9B4)
            (":("        #XE9B5)
            (":-("       #XE9B6)
            (":)"        #XE9B7)
            (":-)"       #XE9B8)
            (":/"        #XE9B9)
            (":\\"       #XE9BA)
            (":3"        #XE9BB)
            (":D"        #XE9BC)
            (":P"        #XE9BD)
            (":>:"       #XE9BE)
            (":<:"       #XE9BF)
            ("<$>"       #XE9C0)
            ("<*"        #XE9C1)
            ("<*>"       #XE9C2)
            ("<+>"       #XE9C3)
            ("<-"        #XE9C4)
            ("<<"        #XE9C5)
            ("<<<"       #XE9C6)
            ("<<="       #XE9C7)
            ("<="        #XE9C8)
            ("<=>"       #XE9C9)
            ("<>"        #XE9CA)
            ("<|>"       #XE9CB)
            ("<<-"       #XE9CC)
            ("<|"        #XE9CD)
            ("<=<"       #XE9CE)
            ("<~"        #XE9CF)
            ("<~~"       #XE9D0)
            ("<<~"       #XE9D1)
            ("<$"        #XE9D2)
            ("<+"        #XE9D3)
            ("<!>"       #XE9D4)
            ("<@>"       #XE9D5)
            ("<#>"       #XE9D6)
            ("<%>"       #XE9D7)
            ("<^>"       #XE9D8)
            ("<&>"       #XE9D9)
            ("<?>"       #XE9DA)
            ("<.>"       #XE9DB)
            ("</>"       #XE9DC)
            ("<\\>"      #XE9DD)
            ("<\">"      #XE9DE)
            ("<:>"       #XE9DF)
            ("<~>"       #XE9E0)
            ("<**>"      #XE9E1)
            ("<<^"       #XE9E2)
            ("<!"        #XE9E3)
            ("<@"        #XE9E4)
            ("<#"        #XE9E5)
            ("<%"        #XE9E6)
            ("<^"        #XE9E7)
            ("<&"        #XE9E8)
            ("<?"        #XE9E9)
            ("<."        #XE9EA)
            ("</"        #XE9EB)
            ("<\\"       #XE9EC)
            ("<\""       #XE9ED)
            ("<:"        #XE9EE)
            ("<->"       #XE9EF)
            ("<!--"      #XE9F0)
            ("<--"       #XE9F1)
            ("<~<"       #XE9F2)
            ("<==>"      #XE9F3)
            ("<|-"       #XE9F4)
            ("<<|"       #XE9F5)
            ("==<"       #XEA00)
            ("=="        #XEA01)
            ("==="       #XEA02)
            ("==>"       #XEA03)
            ("=>"        #XEA04)
            ("=~"        #XEA05)
            ("=>>"       #XEA06)
            ("=/="       #XEA07)
            ("≡≡"        #XEA10)
            ("≡≡≡"       #XEA11)
            ("≡:≡"       #XEA12)
            (">-"        #XEA20)
            (">="        #XEA21)
            (">>"        #XEA22)
            (">>-"       #XEA23)
            (">=="       #XEA24)
            (">>>"       #XEA25)
            (">=>"       #XEA26)
            (">>^"       #XEA27)
            (">>|"       #XEA28)
            (">!="       #XEA29)
            ("??"        #XEA40)
            ("?~"        #XEA41)
            ("?="        #XEA42)
            ("?>"        #XEA43)
            ("???"       #XEA44)
            ("?."        #XEA45)
            ("^="        #XEA48)
            ("^."        #XEA49)
            ("^?"        #XEA4A)
            ("^.."       #XEA4B)
            ("^<<"       #XEA4C)
            ("^>>"       #XEA4D)
            ("^>"        #XEA4E)
            ("\\\\"      #XEA50)
            ("\\>"       #XEA51)
            ("\\/-"      #XEA52)
            ("@>"        #XEA57)
            ("|="        #XEA60)
            ("||"        #XEA61)
            ("|>"        #XEA62)
            ("|||"       #XEA63)
            ("|+|"       #XEA64)
            ("|->"       #XEA65)
            ("|-->"      #XEA66)
            ("|=>"       #XEA67)
            ("|==>"      #XEA68)
            ("|>-"       #XEA69)
            ("|<<"       #XEA6A)
            ("||>"       #XEA6B)
            ("|>>"       #XEA6C)
            ("~="        #XEA70)
            ("~>"        #XEA71)
            ("~~>"       #XEA72)
            ("~>>"       #XEA73)
            ("[["        #XEA80)
            ("]]"        #XEA81)
            ("\">"       #XEA90)
            )))

(defun add-pragmatapro-prettify-symbols-alist ()
  (dolist (alias pragmatapro-prettify-symbols-alist)
    (push alias prettify-symbols-alist)))

(add-hook 'prog-mode-hook
          #'add-pragmatapro-prettify-symbols-alist)

(global-prettify-symbols-mode +1)

(provide 'the-pragmata)

;;; the-pragmata.el ends here
\end{minted}
\section{Navigation}
\label{sec:org40c8018}
\subsection{Completion}
\label{sec:orgdf9d381}
\subsubsection{Packages}
\label{sec:org1125ddf}
\begin{enumerate}
\item Smex - Frecency for command history
\label{sec:org6e5552a}
This package provides a simple mechanism for recording the user's
command history so that it can be used to sort commands by usage. It
is automatically used by Ivy. Note, however, that historian.el will
hopefully replace smex soon, since it provides more functionality in a
more elegant way. See \href{https://github.com/nonsequitur/smex}{1}, \href{https://github.com/PythonNut/historian.el}{2}.

\begin{minted}[]{common-lisp}
(use-package smex)
\end{minted}

\item flx - Fuzzy command matching
\label{sec:orgc599a65}
This package provides a framework for sorting choices in a hopefully
intelligent way based on what the user has typed in, using "fuzzy
matching" (i.e. "ffap" matches "find-file-at-point"). See \href{https://github.com/lewang/flx}{1}.

\begin{minted}[]{common-lisp}
(use-package flx)
\end{minted}

\item Ivy - completing-read on steroids
\label{sec:org36a5c95}
Ivy is a completion and narrowing framework. What does this mean?
By default, Emacs has some basic tab-completion for commands,
files, and so on. Ivy replaces this interface by showing a list of
all the possible options, and narrowing it in an intelligent
way (using smex and flx, if they are installed) as the user inputs
a query. This is much faster.
\begin{enumerate}
\item Setup
\label{sec:org046320d}
\begin{enumerate}
\item Lazy Loading
\label{sec:org2a3cc32}
We'll be making a few patches, so we want to make sure that the
patches aren't loaded until \texttt{ivy} is.

\begin{minted}[]{common-lisp}
(el-patch-feature ivy)
\end{minted}
\item Keymap
\label{sec:orgf790130}
We define a keymap for \texttt{ivy-mode} so we can remap buffer switching
commands when \texttt{ivy} is active.
\begin{minted}[]{common-lisp}
(el-patch-defvar ivy-mode-map
      (let ((map (make-sparse-keymap)))
        (define-key map [remap switch-to-buffer]
          'ivy-switch-buffer)
        (define-key map [remap switch-to-buffer-other-window]
          'ivy-switch-buffer-other-window)
        map)
      "Keymap for `ivy-mode'.")
\end{minted}
\item Minor Mode Patches
\label{sec:org1411fd3}
We patch Ivy to be easily toggle-able, and to restore normal
\texttt{completing-read} functionality if \texttt{ivy-mode} is disabled.
\begin{minted}[]{common-lisp}
(el-patch-define-minor-mode ivy-mode
  "Toggle Ivy mode on or off.
  Turn Ivy mode on if ARG is positive, off otherwise.
  Turning on Ivy mode sets `completing-read-function' to
  `ivy-completing-read'.
  Global bindings:
  \\{ivy-mode-map}
  Minibuffer bindings:
  \\{ivy-minibuffer-map}"
  :group 'ivy
  :global t
  :keymap ivy-mode-map
  :lighter " ivy"
  (if ivy-mode
      (progn
        (setq completing-read-function 'ivy-completing-read)
        (el-patch-splice 2
          (when ivy-do-completion-in-region
            (setq completion-in-region-function 'ivy-completion-in-region))))
    (setq completing-read-function 'completing-read-default)
    (setq completion-in-region-function 'completion--in-region)))
\end{minted}
\item Keybindings
\label{sec:org813c9b6}
\texttt{ivy-resume} lets us jump back into the last completion session, which
is pretty handy.

\begin{minted}[]{common-lisp}
("C-x C-r" . ivy-resume)
\end{minted}
\begin{enumerate}
\item Minibuffer bindings
\label{sec:org803e6a9}
The behavior of Ivy in the minibuffer is a bit unintuitive, so we're
gonna make it a bit more intuitive. In short, tab for navigation,
return for interaction, and \texttt{C-j} to use the current candidate as is.
\begin{minted}[]{common-lisp}
("TAB" . ivy-alt-done)
("<tab>" . ivy-alt-done)
("C-j" . ivy-immediate-done)
\end{minted}
\end{enumerate}
\item Fuzzy matching
\label{sec:org705b6d1}
Fuzzy matching is nice almost everywhere, so we turn it on for all
\texttt{ivy} completions except for \texttt{swiper} (text search), since fuzzy
matching for text is weird. We also raise the \texttt{ivy-flx-limit} so that
it will actually be used.
\begin{minted}[]{common-lisp}
(setq ivy-re-builders-alist
      '((swiper . ivy--regex-plus)
        (t . ivy--regex-fuzzy)))
(setq ivy-flx-limit 2000)
\end{minted}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:org028dbc7}
\begin{minted}[]{common-lisp}
(use-package ivy
  :demand t
  :init
  <<ivy-lazy-load>>
  <<ivy-keymap>>
  <<ivy-mode-patches>>
  (ivy-mode 1)
  :bind (
         <<ivy-global-bindings>>
         :map ivy-minibuffer-map
         <<ivy-minibuffer-bindings>>
         )
  :config
  <<ivy-fuzzy>>
  :delight ivy-mode)
\end{minted}
\end{enumerate}

\item Counsel - Ivy-ized standard Emacs commands
\label{sec:orgf33876b}
Ivy is just a general-purpose completion framework. It can be used
to generate improved versions of many stock Emacs commands. This is
done by the Counsel library. (It also adds a few new commands, such
as \texttt{counsel-git-grep}.)
\begin{enumerate}
\item Setup
\label{sec:org54ad16e}
\begin{enumerate}
\item Bindings
\label{sec:org06beef1}
Counsel is a set of convenient commands based on Ivy meant to improve
the built-in Emacs equivalents. We bind them to the normal Emacs keys
so we can use Ivy nearly everywhere. We also have some other useful
commands for finding and searching within Git repos, and a visual kill
ring for yanking.
\begin{minted}[]{common-lisp}
("M-x" . counsel-M-x)
("C-x C-f" . counsel-find-file)
("C-h f" . counsel-describe-function)
("C-h v" . counsel-describe-variable)
("C-h l" . counsel-load-library)
("C-h C-l" . counsel-find-library)
("C-h S" . counsel-info-lookup-symbol)
("C-x 8 RET" . counsel-unicode-char)
("C-c g" . counsel-git)
("C-c j" . counsel-git-grep)
("C-c k" . counsel-rg)
("M-y" . counsel-yank-pop)
\end{minted}

We also bind a key for use in expression buffers (like
\texttt{eval-expression}) to give us history search.

\begin{minted}[]{common-lisp}
("C-r" . counsel-expression-history)
\end{minted}
\item Find file at point
\label{sec:org9dcbb14}
If there is a valid file at point, \texttt{counsel-find-file} will select
that file by default.
\begin{minted}[]{common-lisp}
(setq counsel-find-file-at-point t)
\end{minted}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:orgd522a07}
\begin{minted}[]{common-lisp}
(use-package counsel
  :bind (;; Use Counsel for common Emacs commands.
         <<counsel-bindings>>
         :map read-expression-map
         <<counsel-expression-bindings>>
         )
  :config
  <<counsel-ffap>>
)
\end{minted}
\end{enumerate}

\item Historian - Remember completion choices
\label{sec:org9f1f777}
Remembers your choices in completion menus.

\begin{minted}[]{common-lisp}
(use-package historian
  :demand t
  :config
  (historian-mode 1))
\end{minted}

\begin{enumerate}
\item \texttt{ivy-historian}
\label{sec:org17b248c}
We use Historian to sort Ivy candidates by frecency+flx.

\begin{enumerate}
\item Setup
\label{sec:orgea6b8d6}
The only configuration we do here is to mess around with how Historian
weights results.
\begin{minted}[]{common-lisp}
(setq ivy-historian-freq-boost-factor 500)
(setq ivy-historian-recent-boost 500)
(setq ivy-historian-recent-decrement 50)
\end{minted}

\item \texttt{use-package} declaration
\label{sec:org65dae1f}
\begin{minted}[]{common-lisp}
(use-package ivy-historian
  :demand t
  :after ivy
  :config
  <<ivy-historian-weights>>
  (ivy-historian-mode 1))
\end{minted}
\end{enumerate}
\end{enumerate}

\item Icicles - Sheesh
\label{sec:org0d8914b}
Icicles is steroids for the steroids. I don't even know everything it
does, so it's not on by default.

\begin{minted}[]{common-lisp}
(use-package icicles)
\end{minted}
\end{enumerate}
\subsection{Finding files}
\label{sec:orgd07158d}
\subsubsection{Dotfile shortcuts}
\label{sec:orge6a6e66}
\begin{enumerate}
\item Prefix key
\label{sec:orgc18a5f0}
We define a custom prefix to provide shortcuts to edit common
dotfiles, and another to edit the file in another window.

\begin{minted}[]{common-lisp}
(defcustom the-find-dotfile-prefix
  (the-join-keys the-prefix "e")
  "Prefix key sequence for opening dotfiles.
  The function `the-register-dotfile' creates a keybinding under
  this prefix, if you ask it to."
  :group 'the
  :type 'string)


(defcustom the-find-dotfile-other-window-prefix
  (the-join-keys the-prefix "o")
  "Prefix key sequencing for opening dotfiles in another window.
The function `the-register-dotfile' creates a keybinding under
this prefix, if you ask it to.")
\end{minted}
\item Registering dotfiles
\label{sec:orgdea0ad1}
By providing a filename (relative to your \texttt{\$HOME}), we define a
function \texttt{the-find-<something>}, where <something> is a cleaned up
version of the basename of the file. You can also specify a keybinding
to be appended to the end of \texttt{the-find-*-prefix}, as well as a pretty
filename if you'd rather not use the automatically generated one.

\begin{minted}[]{common-lisp}
(defmacro the-register-dotfile
    (filename &optional keybinding pretty-filename)
  "Establish functions and keybindings to open a dotfile.
The FILENAME should be a path relative to the user's home
directory. Two interactive functions are created: one to find the
file in the current window, and one to find it in another window.
If KEYBINDING is non-nil, the first function is bound to that key
sequence after it is prefixed by `the-find-dotfile-prefix',
and the second function is bound to the same key sequence, but
prefixed instead by
`the-find-dotfile-other-window-prefix' (provided that the two
prefixes are different).
This is best demonstrated by example. Suppose FILENAME is
\".emacs.d/init.el\", KEYBINDING is \"e i\",
`the-find-dotfile-prefix' is at its default value of \"M-T
e\", and `the-find-dotfile-other-window-prefix' is at its
default value of \"M-T o\". Then `the-register-dotfile' will
create the interactive functions `the-find-init-el' and
`the-find-init-el-other-window', and it will bind them to the
key sequences \"M-T e e i\" and \"M-T o e i\" respectively.
If PRETTY-FILENAME, a string, is non-nil, then it will be used in
place of \"init-el\" in this example. Otherwise, that string will
be generated automatically from the basename of FILENAME."
  (let* ((bare-filename (replace-regexp-in-string ".*/" "" filename))
         (full-filename (concat "~/" filename))
         (defun-name (intern
                      (replace-regexp-in-string
                       "-+"
                       "-"
                       (concat
                        "the-find-"
                        (or pretty-filename
                            (replace-regexp-in-string
                             "[^a-z0-9]" "-"
                             bare-filename))))))
         (defun-other-window-name
           (intern
            (concat (symbol-name defun-name)
                    "-other-window")))
         (docstring (format "Edit file %s."
                            full-filename))
         (docstring-other-window
          (format "Edit file %s, in another window."
                  full-filename))
         (defun-form `(defun ,defun-name ()
                        ,docstring
                        (interactive)
                        (find-file ,full-filename)))
         (defun-other-window-form
           `(defun ,defun-other-window-name ()
              ,docstring-other-window
              (interactive)
              (find-file-other-window ,full-filename)))
         (full-keybinding
          (when keybinding
            (the-join-keys
             the-find-dotfile-prefix
             keybinding)))
         (full-other-window-keybinding
          (the-join-keys
           the-find-dotfile-other-window-prefix
           keybinding)))
    `(progn
       ,defun-form
       ,defun-other-window-form
       (bind-keys
        ,@(when full-keybinding
            `((,full-keybinding . ,defun-name)))
        ,@(when (and full-other-window-keybinding
                     (not (string=
                           full-keybinding
                           full-other-window-keybinding)))
            `((,full-other-window-keybinding
               . ,defun-other-window-name))))
       ;; Return the symbols for the two functions defined.
       (list ',defun-name ',defun-other-window-name))))
\end{minted}

\begin{enumerate}
\item Emacs
\label{sec:org7bb5ba9}
\begin{minted}[]{common-lisp}
(the-register-dotfile ".emacs.d/init.el" "e i")
(the-register-dotfile ".emacs.d/init.local.el" "e l")
\end{minted}

\item Git
\label{sec:org408b2fd}
\begin{minted}[]{common-lisp}
(the-register-dotfile ".gitconfig" "g c")
(the-register-dotfile ".gitexclude" "g e")
(the-register-dotfile ".gitconfig.local" "g l")
\end{minted}

\item Fish
\label{sec:orgd81c923}
\begin{minted}[]{common-lisp}
(the-register-dotfile ".config/fish/config.fish" "f c")
\end{minted}

\item ChunkWM
\label{sec:org7ecb7c6}
\begin{minted}[]{common-lisp}
(the-register-dotfile ".chunkwmrc" "w m")

(defun the-reload-chunkwm ()
  (interactive)
  (async-shell-command "sh ~/.chunkwmrc"))
\end{minted}

\item SKHD
\label{sec:orga519407}
\begin{minted}[]{common-lisp}
(the-register-dotfile ".skhdrc" "h k")
\end{minted}
\end{enumerate}
\end{enumerate}
\subsubsection{Visiting files}
\label{sec:org649d7cc}
\begin{enumerate}
\item Symlinks
\label{sec:orgc36b97f}
Follow symlinks when opening files. This has the concrete impact, for
instance, that when you edit init.el with the shortcut provided by
\texttt{the-register-dotfile} and then later do \texttt{find-file}, you will be in
the THE repository instead of your home directory.

\begin{minted}[]{common-lisp}
(setq find-file-visit-truename t)
\end{minted}

Disable the warning "X and Y are the same file" which normally appears
when you visit a symlinked file by the same name. (Doing this isn't
dangerous, as it will just redirect you to the existing buffer.)

\begin{minted}[]{common-lisp}
(setq find-file-suppress-same-file-warnings t)
\end{minted}

\item VC nonsense
\label{sec:org73ee282}
Disable Emacs' built-in version control handling. This improves
performance and disables some annoying warning messages and prompts,
especially regarding symlinks. I only use Magit, and the \texttt{vc}
machinery does all kinds of annoying stuff with performance and
warnings.

\begin{minted}[]{common-lisp}
(setq vc-handled-backends nil)
\end{minted}

\item Directory hygiene
\label{sec:org342fd54}
Automatically create any nonexistent parent directories when finding a
file. If the buffer for the new file is killed without being saved,
then offer to delete the created directory or directories.

\begin{minted}[]{common-lisp}
(defun the--advice-find-file-automatically-create-directory
    (original-function filename &rest args)
  "Automatically create and delete parent directories of files.
This is an `:override' advice for `find-file' and friends. It
automatically creates the parent directory (or directories) of
the file being visited, if necessary. It also sets a buffer-local
variable so that the user will be prompted to delete the newly
created directories if they kill the buffer without saving it."
  ;; The variable `dirs-to-delete' is a list of the directories that
  ;; will be automatically created by `make-directory'. We will want
  ;; to offer to delete these directories if the user kills the buffer
  ;; without saving it.
  (let ((dirs-to-delete ()))
    ;; If the file already exists, we don't need to worry about
    ;; creating any directories.
    (unless (file-exists-p filename)
      ;; It's easy to figure out how to invoke `make-directory',
      ;; because it will automatically create all parent directories.
      ;; We just need to ask for the directory immediately containing
      ;; the file to be created.
      (let* ((dir-to-create (file-name-directory filename))
             ;; However, to find the exact set of directories that
             ;; might need to be deleted afterward, we need to iterate
             ;; upward through the directory tree until we find a
             ;; directory that already exists, starting at the
             ;; directory containing the new file.
             (current-dir dir-to-create))
        ;; If the directory containing the new file already exists,
        ;; nothing needs to be created, and therefore nothing needs to
        ;; be destroyed, either.
        (while (not (file-exists-p current-dir))
          ;; Otherwise, we'll add that directory onto the list of
          ;; directories that are going to be created.
          (push current-dir dirs-to-delete)
          ;; Now we iterate upwards one directory. The
          ;; `directory-file-name' function removes the trailing slash
          ;; of the current directory, so that it is viewed as a file,
          ;; and then the `file-name-directory' function returns the
          ;; directory component in that path (which means the parent
          ;; directory).
          (setq current-dir (file-name-directory
                             (directory-file-name current-dir))))
        ;; Only bother trying to create a directory if one does not
        ;; already exist.
        (unless (file-exists-p dir-to-create)
          ;; Make the necessary directory and its parents.
          (make-directory dir-to-create 'parents))))
    ;; Call the original `find-file', now that the directory
    ;; containing the file to found exists. We make sure to preserve
    ;; the return value, so as not to mess up any commands relying on
    ;; it.
    (prog1 (apply original-function filename args)
      ;; If there are directories we want to offer to delete later, we
      ;; have more to do.
      (when dirs-to-delete
        ;; Since we already called `find-file', we're now in the buffer
        ;; for the new file. That means we can transfer the list of
        ;; directories to possibly delete later into a buffer-local
        ;; variable. But we pushed new entries onto the beginning of
        ;; `dirs-to-delete', so now we have to reverse it (in order to
        ;; later offer to delete directories from innermost to
        ;; outermost).
        (setq-local the--dirs-to-delete (reverse dirs-to-delete))
        ;; Now we add a buffer-local hook to offer to delete those
        ;; directories when the buffer is killed, but only if it's
        ;; appropriate to do so (for instance, only if the directories
        ;; still exist and the file still doesn't exist).
        (add-hook 'kill-buffer-hook
                  #'the--kill-buffer-delete-directory-if-appropriate
                  'append 'local)
        ;; The above hook removes itself when it is run, but that will
        ;; only happen when the buffer is killed (which might never
        ;; happen). Just for cleanliness, we automatically remove it
        ;; when the buffer is saved. This hook also removes itself when
        ;; run, in addition to removing the above hook.
        (add-hook 'after-save-hook
                  #'the--remove-kill-buffer-delete-directory-hook
                  'append 'local)))))

;; Add the advice that we just defined.
(advice-add #'find-file :around
            #'the--advice-find-file-automatically-create-directory)

;; Also enable it for `find-alternate-file' (C-x C-v).
(advice-add #'find-alternate-file :around
            #'the--advice-find-file-automatically-create-directory)

;; Also enable it for `write-file' (C-x C-w).
;(advice-add #'write-file :around
;            #'the--advice-find-file-automatically-create-directory)

(defun the--kill-buffer-delete-directory-if-appropriate ()
  "Delete parent directories if appropriate.
This is a function for `kill-buffer-hook'. If
`the--advice-find-file-automatically-create-directory' created
the directory containing the file for the current buffer
automatically, then offer to delete it. Otherwise, do nothing.
Also clean up related hooks."
  (when (and
         ;; Stop if there aren't any directories to delete (shouldn't
         ;; happen).
         the--dirs-to-delete
         ;; Stop if `the--dirs-to-delete' somehow got set to
         ;; something other than a list (shouldn't happen).
         (listp the--dirs-to-delete)
         ;; Stop if the current buffer doesn't represent a
         ;; file (shouldn't happen).
         buffer-file-name
         ;; Stop if the buffer has been saved, so that the file
         ;; actually exists now. This might happen if the buffer were
         ;; saved without `after-save-hook' running, or if the
         ;; `find-file'-like function called was `write-file'.
         (not (file-exists-p buffer-file-name)))
    (cl-dolist (dir-to-delete the--dirs-to-delete)
      ;; Ignore any directories that no longer exist or are malformed.
      ;; We don't return immediately if there's a nonexistent
      ;; directory, because it might still be useful to offer to
      ;; delete other (parent) directories that should be deleted. But
      ;; this is an edge case.
      (when (and (stringp dir-to-delete)
                 (file-exists-p dir-to-delete))
        ;; Only delete a directory if the user is OK with it.
        (if (y-or-n-p (format "Also delete directory `%s'? "
                              ;; The `directory-file-name' function
                              ;; removes the trailing slash.
                              (directory-file-name dir-to-delete)))
            (delete-directory dir-to-delete)
          ;; If the user doesn't want to delete a directory, then they
          ;; obviously don't want to delete any of its parent
          ;; directories, either.
          (cl-return)))))
  ;; It shouldn't be necessary to remove this hook, since the buffer
  ;; is getting killed anyway, but just in case...
  (the--remove-kill-buffer-delete-directory-hook))

(defun the--remove-kill-buffer-delete-directory-hook ()
  "Clean up directory-deletion hooks, if necessary.
This is a function for `after-save-hook'. Remove
`the--kill-buffer-delete-directory-if-appropriate' from
`kill-buffer-hook', and also remove this function from
`after-save-hook'."
  (remove-hook 'kill-buffer-hook
               #'the--kill-buffer-delete-directory-if-appropriate
               'local)
  (remove-hook 'after-save-hook
               #'the--remove-kill-buffer-delete-directory-hook
               'local))
\end{minted}

\item Save place\ldots{}
\label{sec:org352298a}
When you open a file, position the cursor at the same place as the
last time you edited the file.

\begin{minted}[]{common-lisp}
(save-place-mode 1)
\end{minted}

\begin{enumerate}
\item \ldots{}and shut up about it
\label{sec:org5ab925a}
Inhibit the message that is usually printed when the `saveplace'
file is written.

\begin{minted}[]{common-lisp}
(el-patch-defun save-place-alist-to-file ()
  (let ((file (expand-file-name save-place-file))
        (coding-system-for-write 'utf-8))
    (with-current-buffer (get-buffer-create " *Saved Places*")
      (delete-region (point-min) (point-max))
      (when save-place-forget-unreadable-files
        (save-place-forget-unreadable-files))
      (insert (format ";;; -*- coding: %s -*-\n"
                      (symbol-name coding-system-for-write)))
      (let ((print-length nil)
            (print-level nil))
        (pp save-place-alist (current-buffer)))
      (let ((version-control
             (cond
              ((null save-place-version-control) nil)
              ((eq 'never save-place-version-control) 'never)
              ((eq 'nospecial save-place-version-control) version-control)
              (t
               t))))
        (condition-case nil
            ;; Don't use write-file; we don't want this buffer to visit it.
            (write-region (point-min) (point-max) file
                          (el-patch-add nil 'nomsg))
          (file-error (message "Saving places: can't write %s" file)))
        (kill-buffer (current-buffer))))))
\end{minted}
\end{enumerate}
\end{enumerate}

\subsubsection{Projects}
\label{sec:org93f2124}
\begin{enumerate}
\item Projectile
\label{sec:org27461e4}
Projectile keeps track of a "project" list, which is automatically
added to as you visit files in Git repositories, Node.js projects,
etc. It then provides commands for quickly navigating between and
within these projects.

\begin{enumerate}
\item Setup
\label{sec:orgd981b09}
\begin{enumerate}
\item Enable projectile globally
\label{sec:org83fa034}
Also, ignore the repos set up by \texttt{straight.el}.
\begin{minted}[]{common-lisp}
(projectile-mode +1)
(defun the-projectile-ignore-projects (project-root)
  (f-descendant-of? project-root (f-join user-emacs-directory "straight/repos")))
(setq projectile-ignored-project-function #'the-projectile-ignore-projects)
\end{minted}
\item Directory-local indexing
\label{sec:org2bfe6cf}
In case your \texttt{.projectile} file is pretty hairy, this allows us to
alter the indexing method as a dirlocal.
\begin{minted}[]{common-lisp}
(defun the-projectile-indexing-method-p (method)
  "Non-nil if METHOD is a safe value for `projectile-indexing-method'."
  (memq method '(native alien)))

(put 'projectile-indexing-method 'safe-local-variable
     #'the-projectile-indexing-method-p)
\end{minted}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:org099bc57}
\begin{minted}[]{common-lisp}
(use-package projectile
  :demand t
  :config
  (setq projectile-mode-line
        '(:eval (format "π[%s]" (projectile-project-name))))
  <<global-projectile>>
  <<projectile-index>>
  )
\end{minted}
\end{enumerate}
\item Counsel Projectile
\label{sec:org32871b9}
Counsel is everywhere! This integrates Projectile commands and Ivy.
\begin{enumerate}
\item \texttt{use-package} declaration
\label{sec:orged73989}
\begin{minted}[]{common-lisp}
(use-package counsel-projectile
  :init
  (setq projectile-switch-project-action #'counsel-projectile-find-file)
  :config
  (counsel-projectile-mode))
\end{minted}
\end{enumerate}
\end{enumerate}
\subsection{Navigation}
\label{sec:org5afaaa4}
\subsubsection{Mark Ring Navigation}
\label{sec:org35e3d5f}
\begin{minted}[]{common-lisp}
;; Make it so that if you provide a negative prefix argument to
;; C-SPC (i.e. like M-- C-SPC), then it will step forwards in the mark
;; ring (whereas C-u C-SPC steps backwards). Based on [1].
;;
;; [1]: http://stackoverflow.com/a/14539202/3538165

(defun the--advice-allow-unpopping-mark
    (set-mark-command &optional arg)
  (interactive "P")
  (if (< (prefix-numeric-value arg) 0)
      ;; If we don't have any marks set, no-op.
      (when mark-ring
        ;; I can't remember how this code works. Sorry.
        (setq mark-ring (cons (copy-marker (mark-marker)) mark-ring))
        (set-marker (mark-marker) (car (last mark-ring)) (current-buffer))
        (when (null (mark t)) (ding))
        (setq mark-ring (nbutlast mark-ring))
        (goto-char (marker-position (car (last mark-ring)))))
    ;; If no prefix argument, or prefix argument is nonnegative, defer
    ;; to the original behavior.
    (funcall set-mark-command arg)))

(advice-add #'set-mark-command :around
            #'the--advice-allow-unpopping-mark)

;; Make it so that if you provide a prefix argument to C-x C-SPC, then
;; it will step forwards in the global mark ring, instead of
;; backwards. Tweaked from the implementation of `pop-global-mark'.

(defun the--advice-allow-unpopping-global-mark
    (pop-global-mark &optional arg)
  (interactive "P")
  (if arg
      (progn
        (or global-mark-ring
            (error "No global mark set"))
        ;; We need to do this earlier than `pop-global-mark' does the
        ;; corresponding action in order to properly undo its
        ;; behavior.
        (setq global-mark-ring (nconc (list (car (last global-mark-ring)))
                                      (butlast global-mark-ring)))
        (while (and global-mark-ring (not (marker-buffer (car (last global-mark-ring)))))
          (setq global-mark-ring (butlast global-mark-ring)))
        (let* ((marker (car (last global-mark-ring)))
               (buffer (marker-buffer marker))
               (position (marker-position marker)))
          (set-buffer buffer)
          (or (and (>= position (point-min))
                   (<= position (point-max)))
              (if widen-automatically
                  (widen)
                (error "Global mark position is outside accessible part of buffer")))
          (goto-char position)
          (switch-to-buffer buffer)))
    (funcall pop-global-mark)))

(advice-add #'pop-global-mark :around
            #'the--advice-allow-unpopping-global-mark)
\end{minted}

\subsubsection{Confirm Reference Jump}
\label{sec:orgc3e47fd}
\begin{minted}[]{common-lisp}
;; When using M-. and friends, always prompt for the identifier (it
;; defaults to the identifier at point). This behavior is more
;; consistent and predictable than the default, which is to jump
;; immediately if there is a valid symbol at point.
(setq xref-prompt-for-identifier t)
\end{minted}

\subsubsection{Faster search highlight}
\label{sec:org63ed860}
\begin{minted}[]{common-lisp}
;; Eliminate the quarter-second delay before I-search matches are
;; highlighted, because delays suck.
(setq lazy-highlight-initial-delay 0)
\end{minted}

\subsubsection{Better search}
\label{sec:orgdd18a22}
\begin{minted}[]{common-lisp}
;; This package provides an enhanced version of Isearch that uses Ivy
;; to display a preview of the results.
(use-package swiper
  :bind (("C-s" . swiper)
         ("C-r" . swiper)))
\end{minted}

\subsubsection{Jump to characters, words, etc.}
\label{sec:org1785d61}
\begin{minted}[]{common-lisp}
;; This package allows you to jump quickly to characters and words
;; visible onscreen.
(use-package avy
  :init

  ;; Create keybindings for common avy commands.

  (defcustom the-avy-prefix
    (the-join-keys the-prefix "g")
    "Prefix key sequence for Avy commands.
Avy keybindings are placed under this prefix by default. nil
means no keybindings are established by default."
    :group 'the
    :type 'string)

  (defmacro the--establish-avy-bindings (&rest specs)
    `(progn
       ,@(cl-mapcan
          (lambda (spec)
            (cl-destructuring-bind (key . command-name) spec
              (let ((custom-name (intern (format "the-%S-keybinding"
                                                 command-name))))
                `((defcustom ,custom-name
                    (when the-avy-prefix
                      (the-join-keys the-avy-prefix ,key))
                    ,(format "Keybinding for `%S', as a string.
nil means no keybinding is established."
                             command-name)
                    :group 'the
                    :type 'string)
                  (when ,custom-name
                    (bind-key ,custom-name #',command-name))))))
          specs)))

  (the--establish-avy-bindings
   ("c" . avy-goto-char)
   ("t" . avy-goto-char-timer)
   ("l" . avy-goto-line)
   ("W" . avy-goto-word-1)
   ("w" . avy-goto-word-0))

  :config

  ;; Use De Bruijn sequences for jump sequences. This allows you to
  ;; fixate on a particular place you want to jump to, and just type
  ;; whatever shows up there.
  (setq avy-style 'de-bruijn))
\end{minted}

\subsubsection{Regexp Search/Replace}
\label{sec:org75a4d48}
\begin{minted}[]{common-lisp}
;; This package highlights matches and previews replacements in query
;; replace.
(use-package visual-regexp
  :demand t
  :bind (;; Replace the regular query replace with the regexp query
         ;; replace provided by this package.
         ("M-%" . vr/query-replace)))

(use-package pcre2el
  :demand t
  :config
  (rxt-global-mode +1))

(defcustom the-regex-engine 'pcre2el
  "The regex engine to use for visual-regexp."
  :group 'the
  :type '(choice
          (const :tag "Emacs" emacs)
          (const :tag "pcre2el" pcre2el)
          (const :tag "Python" python)
          (const :tag "Custom" custom)))

;; This package allows the use of other regexp engines for
;; visual-regexp.
(use-package visual-regexp-steroids
  :demand t
  :after (visual-regexp pcre2el)
  :config

  ;; Use Emacs-style regular expressions by default.
  (setq vr/engine the-regex-engine))
\end{minted}

\subsubsection{Expand Region}
\label{sec:org43a4c89}
\begin{minted}[]{common-lisp}
(use-package expand-region
  :demand t)
\end{minted}
\subsubsection{Vimish Fold}
\label{sec:org13b7e30}
\begin{minted}[]{common-lisp}
(use-package vimish-fold
  :demand t
  :config
  (vimish-fold-global-mode 1))
\end{minted}
\subsection{Search}
\label{sec:orge3dda0b}
\subsubsection{Regular Expressions}
\label{sec:org65bf303}
\begin{enumerate}
\item Rx
\label{sec:orgcce4967}
A prescription for your regex woes. Don't write obscure regex
syntax. Describe the regex you want, then generate the bizarre
incantation you need.

\begin{minted}[]{common-lisp}
(use-package rx)
\end{minted}
\end{enumerate}
\section{Writing}
\label{sec:orgd9f597e}
\subsection{Org Mode Customization}
\label{sec:orgc6ad546}
\subsubsection{Global Outline Mode}
\label{sec:org687c836}
Outlines work for just about any structured text imaginable, from code
to prose. If it's got something that Emacs thinks is a paragraph, it
works. When you need a high-level overview, it's hard to beat this.

\begin{minted}[]{common-lisp}
(define-globalized-minor-mode global-outline-minor-mode
  outline-minor-mode outline-minor-mode)

(global-outline-minor-mode +1)
\end{minted}

\subsubsection{Org}
\label{sec:org62e6370}
Org is a hugely expansive framework (a.k.a. collection of hacks) for
organizing information, notes, tasks, calendars, and anything else
related to Org-anization.

\begin{enumerate}
\item Setup
\label{sec:org54d31e5}
\begin{enumerate}
\item Version Hack
\label{sec:org42e95cd}
Because \texttt{straight.el} runs Org directly from a Git repo, the
autoloads Org uses to identify its version are not generated in
the way that it expects. This causes it to either a) fail to
determine its version at all or b) incorrectly report the version
of the built-in Org which ships with Emacs. This causes some
issues down the line, so we have to trick Org. This is how we do it.

First, we have to get the Git version, here represented by a short
hash of the current commit.

\begin{minted}[]{common-lisp}
(defun the-org-git-version ()
  (let ((git-repo
         (f-join user-emacs-directory "straight/repos/org")))
    (s-trim (git-run "describe"
                     "--match=release\*"
                     "--abbrev=6"
                     "HEAD"))))
\end{minted}

\begin{minted}[]{common-lisp}
(defun the-org-release ()
  (let ((git-repo
         (f-join user-emacs-directory "straight/repos/org")))
    (s-trim (s-chop-prefix "release_"
                           (git-run "describe"
                                    "--match=release\*"
                                    "--abbrev=0"
                                    "HEAD")))))
\end{minted}

Next, we need to define \texttt{org-git-version} and \texttt{org-release} eagerly.

\begin{minted}[]{common-lisp}
<<org-version>>
<<org-release>>
(defalias #'org-git-version #'the-org-git-version)
(defalias #'org-release #'the-org-release)
(provide 'org-version)
\end{minted}

\item Todo Sequence
\label{sec:org03c4bdb}
We use an augmented set of todo states, including TODO, IN-PROGRESS,
WAITING, and the done states DONE and CANCELED.
\begin{minted}[]{common-lisp}
(setq org-todo-keywords
      '((sequence
         "BACKLOG(b!)"
         "TODO(t!)"
         "NEXT(n)"
         "IN-PROGRESS(i!)"
         "|"
         "DONE(d!)")
        (sequence
         "WAITING(w@/!)"
         "HOLD(h@/!)"
         "|"
         "CANCELED(c@)")
        (type
         "PHONE(p!)"
         "MEETING(m!)")))

(setq org-todo-keyword-faces
      (quote (("TODO" :foreground "red" :weight bold)
              ("NEXT" :foreground "blue" :weight bold)
              ("IN-PROGRESS" :foreground "red" :weight bold)
              ("DONE" :foreground "forest green" :weight bold)
              ("WAITING" :foreground "orange" :weight bold)
              ("HOLD" :foreground "magenta" :weight bold)
              ("CANCELED" :foreground "forest green" :weight bold)
              ("MEETING" :foreground "forest green" :weight bold)
              ("PHONE" :foreground "forest green" :weight bold))))

(setq org-todo-state-tags-triggers
      (quote (("CANCELED" ("CANCELED" . t))
              ("WAITING" ("WAITING" . t))
              ("HOLD" ("WAITING") ("HOLD" . t))
              (done ("WAITING") ("HOLD"))
              ("TODO" ("WAITING") ("CANCELLED") ("HOLD"))
              ("NEXT" ("WAITING") ("CANCELLED") ("HOLD"))
              ("DONE" ("WAITING") ("CANCELLED") ("HOLD")))))

(setq org-use-fast-todo-selection t)
(setq org-treat-S-cursor-todo-selection-as-state-change nil)
(setq org-archive-location (f-expand "archive/%s::* Archived Tasks" org-directory))
\end{minted}
\item Bindings
\label{sec:org419ec10}

First, we want to set up some recommended bindings as specified in the
Org manual.

\begin{minted}[]{common-lisp}
("C-c a" . org-agenda)
("C-c c" . org-capture)
("C-c l" . org-store-link)
("C-c b" . org-switchb)
\end{minted}

First, we move the Org bindings for \texttt{org-shift*} from the \texttt{S-} prefix
to \texttt{C-}.

\begin{minted}[]{common-lisp}
("S-<left>" . nil)
("S-<right>" . nil)
("S-<up>" . nil)
("S-<down>" . nil)
("C-<left>" . org-shiftleft)
("C-<right>" . org-shiftright)
("C-<up>" . org-shiftup)
("C-<down>" . org-shiftdown)
\end{minted}

By default, Org maps \texttt{org-(backward/forward)-paragraph}, but only maps
it to the keys we overrode for shift up and down. We'll remap all
instances so that our existing bindings for those functions will work
as expected.

\begin{minted}[]{common-lisp}
([remap backward-paragraph] . org-backward-paragraph)
([remap forward-paragraph] . org-forward-paragraph)
\end{minted}

Finally, we'll set up a convenient binding for inserting headings.

\begin{minted}[]{common-lisp}
("M-RET" . org-insert-heading)
("M-<return>" . org-insert-heading)
("M-S-RET" . org-insert-todo-heading)
("M-S-<return>" . org-insert-todo-heading)
\end{minted}

\item Settings
\label{sec:org48f9910}
\texttt{org-insert-headline} will split your content by default, which is
pretty dumb. We therefore set it to create a new heading, instead. We
also activate \texttt{org-indent-mode} for more beautiful documents.

We also set Org exports to occur asynchronously whenever possible.

Finally, we set up refile to use outline path completion for easier
refiling.

\begin{minted}[]{common-lisp}
(setq org-insert-heading-respect-content t)
(add-hook 'org-mode-hook #'org-indent-mode)
(setq org-export-in-background t)
(setq org-refile-use-outline-path t
      org-outline-path-complete-in-steps nil)
(setq org-log-into-drawer t)
(setq org-special-ctrl-a/e t
      org-special-ctrl-k t)
(setq org-return-follows-link t)
;(add-to-list 'org-latex-packages-alist '("" "minted"))
(setq org-latex-listings 'minted)

(setq org-latex-pdf-process
      '("pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))
\end{minted}

\item Tags
\label{sec:orgc360cf5}
We set up some useful tags we'd like available in any Org buffer.
\begin{minted}[]{common-lisp}
(setq org-tag-persistent-alist
      '((:startgroup . nil)
        ("@work" . ?w)
        ("@home" . ?h)
        ("@phone" . ?p)
        ("@mail" . ?m)
        (:endgroup . nil)
        ("ansible" . ?a)
        ("epic" . ?e)
        ("linux" . ?l)
        ("noexport" . ?n)
        ("crypt" . ?c)
        ))
\end{minted}
\item Default Org Directory
\label{sec:org6fd95e0}
We stick our Org files in a new directory in the home directory by
default.
\begin{minted}[]{common-lisp}
(setq org-directory "~/org")
\end{minted}
\item Capture Templates
\label{sec:org87887d2}
\begin{minted}[]{common-lisp}
(setq org-capture-templates
      '(("t" "Todo" entry (file+headline "~/org/inbox.org" "Tasks")
         "* BACKLOG %?\n %T\n  %i\n  %a")
        ("m" "Meeting" entry (file+headline "~/org/work.org" "Meetings")
         "* MEETING with %? :MEETING:\n%U" :clock-in t :clock-resume t)
        ("p" "Phone call" entry (file+headline "~/org/work.org" "Phone Calls")
         "* PHONE %? :PHONE:\n%U" :clock-in t :clock-resume t)
        ("g" "Groceries" entry (file+headline "~/org/groceries.org" "Groceries")
         "* %?\nEntered on %U\n  %i")
        ("w" "Work" entry (file+headline "~/org/work.org" "Tasks")
         "* TODO %?\n %T\n %i\n %a")
        ("h" "Home" entry (file+headline "~/org/home.org" "Tasks")
         "* TODO %?\n %i")))

        (setq org-refile-targets
              '((org-agenda-files :maxlevel . 3)))
\end{minted}
\item Utilities
\label{sec:org04a9aa6}
\begin{enumerate}
\item Recursively sort buffer entries alphabetically
\label{sec:orgf1868f0}
\begin{minted}[]{common-lisp}
(defun the-org-sort-ignore-errors ()
  (condition-case x
      (org-sort-entries nil ?a)
    (user-error)))

(defun the-org-sort-buffer ()
  "Sort all entries in the Org buffer recursively in alphabetical order."
  (interactive)
  (org-map-entries #'the-org-sort-ignore-errors))
\end{minted}

\item Archive dead tasks
\label{sec:org64fecbd}
If tasks are marked DONE, and either have no deadline or the deadline
has passed, archive it.

\begin{minted}[]{common-lisp}
(defun the-org-past-entries ()
  (when (and (string= (org-get-todo-state) "DONE")
             (let ((deadline (org-entry-get (point) "DEADLINE")))
               (or (null deadline)
                   (time-less-p (org-time-string-to-time deadline)
                                (current-time)))))
    (org-archive-subtree)
    (setq org-map-continue-from (line-beginning-position))))


(defun the-org-archive-past ()
  "Archive DONE items with deadlines either missing or in the past."
  (interactive)
  (org-map-entries #'the-org-past-entries))
\end{minted}

\item Pretty bullets
\label{sec:org3a2d08d}
We use \texttt{org-bullets} to make our outlines prettier. There's some minor
alignment weirdness with my font, so I may need to specify the bullet
codepoints, later.
\begin{minted}[]{common-lisp}
(use-package org-bullets
  :init
  (add-hook 'org-mode-hook 'org-bullets-mode))
\end{minted}

\item Dropbox integration
\label{sec:org66db4c6}
If \texttt{\textasciitilde{}/org/} doesn't exist, but \texttt{\textasciitilde{}/Dropbox/org} does, symlink the
latter to the former.
\begin{minted}[]{common-lisp}
(if (and
     (not (f-exists? org-directory))
     (f-directory? "~/Dropbox/org"))
    (f-symlink "~/Dropbox/org" org-directory))
\end{minted}
\end{enumerate}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:org1f92767}

\begin{minted}[]{common-lisp}
(use-package org
  :straight org-plus-contrib
  :demand t
  :bind (
         <<basic-bindings>>
         :map org-mode-map
         <<org-mode-bindings>>
         <<org-mode-remaps>>
         <<org-mode-heading>>
         )
  :config
  <<org-version-definitions>>
  <<org-dir>>
  <<org-capture>>
  <<org-bullets>>
  <<org-settings>>
  <<org-sort-buffer>>
  <<org-archive-past>>
  <<todo-states>>
  <<org-dropbox>>
  <<org-tags>>
  (with-eval-after-load 'org-src
    (diminish 'org-src-mode))
  (with-eval-after-load 'org-indent
    (diminish 'org-indent-mode))
  :delight
  (org-mode "Ο")
  )
\end{minted}
\end{enumerate}

\subsubsection{Org Agenda}
\label{sec:org71d4c8c}
Org Agenda is for generating a more useful consolidated summary of all
or some of your tasks, according to their metadata.

\begin{enumerate}
\item Setup
\label{sec:org9a64557}
\begin{enumerate}
\item Bindings
\label{sec:org166f41f}
Analogously to our bindings for regular org files, we'll also move
things off of \texttt{S-} and onto \texttt{C-}.

\begin{minted}[]{common-lisp}
("S-<up>" . nil)
("S-<down>" . nil)
("S-<left>" . nil)
("S-<right>" . nil)
("C-<left>" . org-agenda-do-date-earlier)
("C-<right>" . org-agenda-do-date-later)
\end{minted}

\item Window Splitting
\label{sec:org8eb311e}
We want Org Agenda to split the window into two tall windows, rather
than two wide windows stacked.

\begin{minted}[]{common-lisp}
(defun the--advice-org-agenda-split-horizontally (org-agenda &rest args)
  "Make `org-agenda' split horizontally, not vertically, by default.
  This is an `:around' advice for `org-agenda'. It commutes with
  `the--advice-org-agenda-default-directory'."
  (let ((split-height-threshold nil))
    (apply org-agenda args)))

(advice-add #'org-agenda :around
            #'the--advice-org-agenda-split-horizontally)
\end{minted}

\item Default Directory
\label{sec:orgeb5d31b}
If \texttt{org-directory} exists, set \texttt{default-directory} to its value in the
agenda so that things like \texttt{find-file} work sensibly.

\begin{minted}[]{common-lisp}
(defun the--advice-org-agenda-default-directory
    (org-agenda &rest args)
  "If `org-directory' exists, set `default-directory' to it in the agenda.
  This is an `:around' advice for `org-agenda'. It commutes with
  `the--advice-org-agenda-split-horizontally'."
  (let ((default-directory (if (f-exists? org-directory)
                               org-directory
                             default-directory)))
    (apply org-agenda args)))

(advice-add #'org-agenda :around
            #'the--advice-org-agenda-default-directory)
\end{minted}

\item Settings
\label{sec:org871329c}
\begin{minted}[]{common-lisp}
(setq org-agenda-files '("~/org"))
(setq org-agenda-skip-scheduled-if-done t
      org-agenda-skip-deadline-if-done t)
\end{minted}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:org1e12014}
\begin{minted}[]{common-lisp}
(use-package org-agenda
  :straight org-plus-contrib
  :after (org)
  :demand t
  :bind (:map org-agenda-mode-map
         <<org-agenda-bindings>>
         )
  :init
  <<agenda-files>>
  :config
  <<agenda-window-split>>
  <<agenda-default-directory>>
  :delight
  (org-agenda-mode "Οα ")
  )
\end{minted}
\end{enumerate}
\subsubsection{Clocking}
\label{sec:org4909cb0}
\begin{minted}[]{common-lisp}
(use-package org-clock
  :straight org-plus-contrib
  :after (org-agenda)
  :bind (:map org-mode-map
         ("C-c C-x C-i" . org-clock-in)
         ("C-c C-x C-o" . org-clock-out))
  :demand t)

(use-package org-pomodoro
  :after (org-clock)
  :bind
  ("M-T c p" . org-pomodoro)
  :demand t)

\end{minted}
\subsubsection{Org Encryption}
\label{sec:org336b0f0}
\begin{minted}[]{common-lisp}
(use-package org-crypt
  :straight org-plus-contrib
  :demand t
  :config
  (org-crypt-use-before-save-magic)
  (setq org-tags-exclude-from-inheritance '("crypt"))
  (setq org-crypt-key "17F07DF3086C4BBFA5799F38EF21DED4826AAFCF"))
\end{minted}
\subsubsection{Org Journal}
\label{sec:orgcd5fdba}
Keeping a regular record of what's going on at work (programmer's
journal) and at home (personal journal) can be a useful habit, so
let's give it a shot.
\begin{minted}[]{common-lisp}
(use-package org-journal
  :demand t
  :config
  (setq org-journal-dir (f-expand "journal" org-directory))
  (setq org-journal-enable-encryption t))
\end{minted}
\subsubsection{Context-Aware Capture and Agenda}
\label{sec:orgb79f98c}
\begin{minted}[]{common-lisp}
(use-package org-context
  :demand t
  :config
  (setq org-context-capture-shortcut
        '((todo
           "t" "Todo"
           entry (file+headline place-holder "Todos")
           "* TODO %?\n OPENED: %U by %n\n FILE: %a")
          (question
           "q" "Question"
           entry (file+headline place-holder "Questions")
           "* QUESTION %?\n OPENED: %U by %n\n FILE: %a")))
  (org-context-activate))
\end{minted}
\subsubsection{Extra Export Packages}
\label{sec:orga74b0ae}
In order to correctly export Org files to certain formats, we need
some additional tools.
\begin{enumerate}
\item \texttt{htmlize}
\label{sec:org6190295}
Used to convert symbols and such to HTML equivalents.
\begin{minted}[]{common-lisp}
(use-package htmlize)
\end{minted}
\end{enumerate}
\subsubsection{Org-mode Config Settings}
\label{sec:org3e770df}
Our config files live in \texttt{the-lib-directory}, but our org source files
live in \texttt{the-org-lib-directory}. Unless I decide to start loading org
files directly (which is doable if a touch annoying, at times), for
now I want the \texttt{:tangle} attribute set for me automatically as long as
I'm working on one of THE's lib files.

Additionally, I'd like to regenerate the documentation on save so
things will always be up to date.

\begin{minted}[]{common-lisp}
(defun the-in-the-org-lib-p ()
  (and (f-this-file)
       (f-child-of? (f-this-file) the-org-lib-directory)))

(defun the-update-doc ()
  "Update the readme."
  (interactive)
  (save-window-excursion
    (progn
      (find-file the-doc-source-file)
      (org-md-export-to-markdown)
      (org-latex-export-to-pdf))))


(defun the-org-lib-hook ()
  (if (the-in-the-org-lib-p)
      (progn
        (setq-local org-babel-default-header-args:emacs-lisp
                    `((:tangle . ,(f-expand (f-swap-ext (f-filename (f-this-file)) "el") the-lib-directory))
                      (:noweb . "yes"))))))

  (add-hook 'org-mode-hook 'the-org-lib-hook)
\end{minted}

Finally, I'd like to automatically tangle the files on save.

\begin{minted}[]{common-lisp}
(defun the-org-lib-tangle-hook ()
  (if (the-in-the-org-lib-p)
      (org-babel-tangle)))

(add-hook 'after-save-hook 'the-org-lib-tangle-hook)
\end{minted}
\subsubsection{\texttt{org-tree-slide}}
\label{sec:org4443201}
\begin{minted}[]{common-lisp}
(use-package org-tree-slide
  :config
  (org-tree-slide-presentation-profile)
  (defun the-presentation-start ()
    (text-scale-set 5)
    (setq org-confirm-babel-evaluate nil)
    (setq ns-use-native-fullscreen t)
    (disable-theme 'gruvbox)
    (load-theme 'leuven)
    (toggle-frame-fullscreen))
  (defun the-presentation-stop ()
    (text-scale-set 0)
    (setq org-confirm-babel-evaluate t)
    (disable-theme 'leuven)
    (load-theme 'gruvbox)
    (setq ns-use-native-fullscreen nil))
  (add-hook 'org-tree-slide-play-hook #'the-presentation-start)
  (add-hook 'org-tree-slide-stop-hook #'the-presentation-stop)
  )
\end{minted}
\subsection{Editing Prose}
\label{sec:orgf891e8b}
\subsubsection{AUCTeX}
\label{sec:orgec14906}
\begin{minted}[]{common-lisp}
(use-package tex
  :demand t
  :straight auctex
  :config
  (setq TeX-auto-save t)
  (setq TeX-parse-self t))

(use-package tex-buf
  :straight auctex
  :config

  ;; Save buffers automatically when compiling, instead of prompting.
  (setq TeX-save-query nil))

(use-package latex
  :straight auctex
  :config

  ;; Don't be afraid to break inline math between lines.
  (setq LaTeX-fill-break-at-separators nil))

(use-package auctex-latexmk
  :demand t
  :after tex
  :config
  (auctex-latexmk-setup))

(use-package company-auctex
  :demand t
  :after tex
  :config

  ;; Enable the functionality of `company-auctex'.
  (company-auctex-init))
\end{minted}
\subsubsection{Flyspell}
\label{sec:orgac74b5f}
Flyspell is Flycheck but for spelling. Simple as.
\begin{minted}[]{common-lisp}
(use-package flyspell
  :bind* (("M-T ] s" . flyspell-goto-next-error))
  :diminish (flyspell-mode . "φ"))
\end{minted}
\subsection{Formatting}
\label{sec:orgc313916}
\subsubsection{Formatting Options}
\label{sec:orgce6c5ab}
\begin{enumerate}
\item Formatting
\label{sec:orgaaff617}
\begin{enumerate}
\item Sanity
\label{sec:orge65b32e}
Don't use tabs for indentation, even in deeply indented lines.

\begin{minted}[]{common-lisp}
(setq-default indent-tabs-mode nil)
\end{minted}

Sentences end with one space, not two. We're not French typographers,
so cut it out.

\begin{minted}[]{common-lisp}
(setq sentence-end-double-space nil)
\end{minted}

80 columns is the correct line length. Fight me.
\begin{minted}[]{common-lisp}
(setq-default fill-column 80)
\end{minted}

\item Whitespace
\label{sec:orgebdd0d4}
Trim trailing whitespace on save. This will get rid of end-of-line
whitespace, and reduce the number of blank lines at the end of the
file to one.

We don't always want this (though I almost always do), so we create a
variable which is set globally, but which can be overridden on a
per-file or per-directory basis.

\begin{minted}[]{common-lisp}
(defvar the-delete-trailing-whitespace t
  "If non-nil, delete trailing whitespace on save.")

(put 'the-delete-trailing-whitespace
     'safe-local-variable #'booleanp)
\end{minted}

And now we have a little helper to delete whitespace according to our
variable.

\begin{minted}[]{common-lisp}
(defun the--maybe-delete-trailing-whitespace ()
  "Maybe delete trailing whitespace in buffer.
Trailing whitespace is only deleted if variable
`the-delete-trailing-whitespace' if non-nil."
  (when the-delete-trailing-whitespace
    (delete-trailing-whitespace)))
\end{minted}

Now we make sure whitespace is (maybe) deleted on save.

\begin{minted}[]{common-lisp}
(add-hook 'before-save-hook
          #'the--maybe-delete-trailing-whitespace)
\end{minted}

Finally, always end files with a newline.

\begin{minted}[]{common-lisp}
(setq require-final-newline t)
\end{minted}

\begin{enumerate}
\item \texttt{long-lines-mode}
\label{sec:org94e98d4}
We define a minor mode for configuring \texttt{whitespace-mode} to highlight
long lines. Enabling the mode will highlight characters beyond the
fill column (80 columns, by default).

\begin{minted}[]{common-lisp}
(define-minor-mode the-long-lines-mode
  "When enabled, highlight long lines."
  nil nil nil
  (if the-long-lines-mode
      (progn
        (setq-local whitespace-style '(face lines-tail))
        (whitespace-mode 1))
    (whitespace-mode -1)
    (kill-local-variable 'whitespace-style)))
\end{minted}
\end{enumerate}

\item Line Wrapping
\label{sec:orga9c3e10}
When editing text (i.e., not code), we want to automatically keep
lines a reasonable length (<80 columns).

\begin{minted}[]{common-lisp}
(add-hook 'text-mode-hook #'auto-fill-mode)
\end{minted}

\texttt{fill-paragraph} is pretty good, but some structured markup (like
markdown) doesn't always play nice. \texttt{filladapt} will fill in these
gaps. However, we shut it off in Org because Org already has its own
version of the functionality of \texttt{filladapt}, and they don't agree with
each other.

\begin{minted}[]{common-lisp}
(use-package filladapt
  :demand t
  :config
  (add-hook 'text-mode-hook #'filladapt-mode)
  (add-hook 'org-mode-hook #'turn-off-filladapt-mode))
\end{minted}

Use an adaptive fill prefix when visually wrapping too-long lines.
This means that if you have a line that is long enough to wrap
around, the prefix (e.g. comment characters or indent) will be
displayed again on the next visual line. We turn it on everywhere by
lifting it up to a global minor mode.

\begin{minted}[]{common-lisp}
(use-package adaptive-wrap
  :demand t
  :config
  (define-globalized-minor-mode global-adaptive-wrap-prefix-mode
    adaptive-wrap-prefix-mode adaptive-wrap-prefix-mode)

  (global-adaptive-wrap-prefix-mode))
\end{minted}

\item EditorConfig
\label{sec:orgd60a906}
EditorConfig is a tool for establishing and maintaining consistent
code style in editors and IDEs which support it (most of the major
ones have a plugin).

\begin{minted}[]{common-lisp}
(use-package editorconfig)
\end{minted}

\item Utilities
\label{sec:org9b1d849}
Like \texttt{reverse-region}, but works characterwise rather than linewise.

\begin{minted}[]{common-lisp}
(defun the-reverse-characters (beg end)
  "Reverse the characters in the region from BEG to END.
Interactively, reverse the characters in the current region."
  (interactive "*r")
  (insert
   (reverse
    (delete-and-extract-region
     beg end))))
\end{minted}
\end{enumerate}
\end{enumerate}
\subsubsection{Indentation}
\label{sec:orgbc5c33d}
\begin{enumerate}
\item Aggressive Indent
\label{sec:orgf4912e6}
Assuming your indentation is consistent, this will keep it correct
without any additional work.
\begin{enumerate}
\item Setup
\label{sec:orgef6c758}
\begin{enumerate}
\item Local Variable
\label{sec:org78a4323}
Here, we set up \texttt{aggressive-indent-mode} as a variable we can set on a
file- or directory-local level.
\begin{minted}[]{common-lisp}
(put 'aggressive-indent-mode 'safe-local-variable #'booleanp)
\end{minted}
\item Slow mode
\label{sec:orgab95cee}
We register \texttt{aggressive-indent} with our slow mode, allowing us to
disabled reindentation on save for situations in which reindentation
is expensive. Note that
\texttt{aggressive-indent-{}-proccess-changed-list-and-indent} is not a
typo. Or rather, it is, but it's in the actual package, not on us.
\begin{minted}[]{common-lisp}
(defun the-aggressive-indent-toggle-slow ()
  "Slow down `aggressive-indent' by disabling reindentation on save.
This is done in `the-slow-indent-mode'."
  (add-hook 'aggressive-indent-mode-hook
            #'the-aggressive-indent-toggle-slow)
  (if (or the-slow-indent-mode (not aggressive-indent-mode))
      (remove-hook 'before-save-hook
                   #'aggressive-indent--proccess-changed-list-and-indent
                   'local)
    (add-hook 'before-save-hook
              #'aggressive-indent--proccess-changed-list-and-indent
              nil 'local)))

(add-hook 'the-slow-indent-mode #'the-aggressive-indent-toggle-slow)
\end{minted}
\end{enumerate}
\item \texttt{use-package} declaration
\label{sec:orgce99b48}
\begin{minted}[]{common-lisp}
(use-package aggressive-indent
  :init
  <<agg-indent-local>>
  :config
  <<agg-indent-slow>>
  :diminish aggressive-indent-mode)

\end{minted}
\end{enumerate}
\end{enumerate}
\section{Reading}
\label{sec:orgb89336f}
\subsection{PDF Functionality}
\label{sec:org31a7e35}
\subsubsection{\texttt{pdf-tools}}
\label{sec:orgff4fb06}
DocView is the built-in PDF viewer in Emacs, but it's a bit meh.
\texttt{pdf-tools} is significantly nicer, with much better support for
in-document hyperlinks and fancy things like that. It does require
compilation of an external library, though.

\begin{minted}[]{common-lisp}
(use-package pdf-tools
  :init
  (pdf-tools-install)
  (setq pdf-view-midnight-colors '("#fe8019" . "#1d2021"))
  (add-hook 'pdf-view-mode-hook #'pdf-view-midnight-minor-mode))
\end{minted}
\section{Version Control}
\label{sec:orgb796801}
\subsection{Git integration}
\label{sec:org8cc8aa1}
\subsubsection{Direct Interaction}
\label{sec:org9c832b0}
For Elisp purposes, we occasionally need to get some piece of
information from Git. We do this using \texttt{git.el}, a dead-simple git
interaction library.

\begin{minted}[]{common-lisp}
(use-package git
  :demand t)
\end{minted}

\subsubsection{Magit}
\label{sec:org7e76846}
Magit is one of the Emacs killer apps. It's a Git porcelain which
makes interacting with Git intuitive, instructive, and quick.

\begin{minted}[]{common-lisp}
(use-package magit
  :bind (;; Add important keybindings for Magit as described in the
         ;; manual [1].
         ;;
         ;; [1]: https://magit.vc/manual/magit.html#Getting-Started
         ("C-x g" . magit-status)
         ("C-x M-g" . magit-dispatch-popup))
  :init

  ;; Suppress the message we get about "Turning on
  ;; magit-auto-revert-mode" when loading Magit.
  (setq magit-no-message '("Turning on magit-auto-revert-mode..."))

  :config

  ;; Enable the C-c M-g shortcut to go to a popup of Magit commands
  ;; relevant to the current file.
  (global-magit-file-mode +1)

  ;; The default location for git-credential-cache is in
  ;; ~/.config/git/credential. However, if ~/.git-credential-cache/
  ;; exists, then it is used instead. Magit seems to be hardcoded to
  ;; use the latter, so here we override it to have more correct
  ;; behavior.
  (unless (file-exists-p "~/.git-credential-cache/")
    (let* ((xdg-config-home (or (getenv "XDG_CONFIG_HOME")
                                (expand-file-name "~/.config/")))
           (socket (expand-file-name "git/credential/socket" xdg-config-home)))
      (setq magit-credential-cache-daemon-socket socket))))

\end{minted}

** \texttt{git-commit}
\begin{minted}[]{common-lisp}
;; Allows editing Git commit messages from the command line (i.e. with
;; emacs or emacsclient as your core.editor).
(use-package git-commit
  :init

  ;; Lazy-load `git-commit'.

  (el-patch-feature git-commit)

  (el-patch-defconst git-commit-filename-regexp "/\\(\
     \\(\\(COMMIT\\|NOTES\\|PULLREQ\\|TAG\\)_EDIT\\|MERGE_\\|\\)MSG\
     \\|BRANCH_DESCRIPTION\\)\\'")

  (el-patch-defun git-commit-setup-check-buffer ()
    (and buffer-file-name
         (string-match-p git-commit-filename-regexp buffer-file-name)
         (git-commit-setup)))

  (el-patch-define-minor-mode global-git-commit-mode
    "Edit Git commit messages.
     This global mode arranges for `git-commit-setup' to be called
     when a Git commit message file is opened.  That usually happens
     when Git uses the Emacsclient as $GIT_EDITOR to have the user
     provide such a commit message."
    :group 'git-commit
    :type 'boolean
    :global t
    :init-value t
    :initialize (lambda (symbol exp)
                  (custom-initialize-default symbol exp)
                  (when global-git-commit-mode
                    (add-hook 'find-file-hook 'git-commit-setup-check-buffer)))
    (if global-git-commit-mode
        (add-hook  'find-file-hook 'git-commit-setup-check-buffer)
      (remove-hook 'find-file-hook 'git-commit-setup-check-buffer)))

  (global-git-commit-mode 1)

  :config

  ;; Wrap summary at 50 characters as per [1].
  ;;
  ;; [1]: http://chris.beams.io/posts/git-commit/
  (setq git-commit-summary-max-length 50))
\end{minted}

\begin{enumerate}
\item Github Integration
\label{sec:org1bd3720}
\begin{minted}[]{common-lisp}
(use-package magithub
  :demand t
  :after magit
  :config (magithub-feature-autoinject t))
\end{minted}
\end{enumerate}

\subsubsection{Get link to commit or source line}
\label{sec:orga78af24}
Occasionally it's useful to send a link to a specific commit or source
line on <repo host of choice>, and this package makes that relatively
easy.
\begin{minted}[]{common-lisp}
(use-package git-link
  :demand t
  :bind (("C-c C-g l" . git-link)))
\end{minted}
\section{Programming Utilities}
\label{sec:org793192d}
\subsection{Syntax Checking}
\label{sec:orgcb2c70b}
\subsubsection{Flycheck}
\label{sec:org44a41ae}
Flycheck provides a framework for in-buffer error and warning
highlighting, or more generally syntax checking. It comes with a large
number of checkers pre-defined, and other packages define more.

\begin{enumerate}
\item Settings
\label{sec:org0e81b7d}
\begin{enumerate}
\item Enable Flycheck Globally
\label{sec:org02c8d3e}
Enable Flycheck in all buffers, but also allow for disabling it
per-buffer.

\begin{minted}[]{common-lisp}
(global-flycheck-mode +1)
(put 'flycheck-mode 'safe-local-variable #'booleanp)
\end{minted}

\item Disable Flycheck in the modeline
\label{sec:org2c58402}
It's honestly more distracting than anything,

\begin{minted}[]{common-lisp}
(setq flycheck-mode-line nil)
\end{minted}
\end{enumerate}

\item \texttt{use-package} declaration
\label{sec:org9c5a2b8}
\begin{minted}[]{common-lisp}
(use-package flycheck
  :defer 3
  :config
  <<flycheck-global>>
  <<no-flycheck-modeline>>
  )
\end{minted}
\end{enumerate}
\subsection{Auto-completion}
\label{sec:orgaca528a}
\subsubsection{Company Settings}
\label{sec:org2c3607e}
\begin{itemize}
\item Show completions instantly, rather than after half a second.
\item Show completions after typing three characters.
\item Show a maximum of 10 suggestions. This is the default but I think
it's best to be explicit.
\item Always display the entire suggestion list onscreen, placing it above
the cursor if necessary.
\item Always display suggestions in the tooltip, even if there is only
one. Also, don't display metadata in the echo area (this conflicts
with ElDoc).
\item Show quick-reference numbers in the tooltip (select a completion
with M-1 through M-0).
\item Prevent non-matching input (which will dismiss the completions
menu), but only if the user interacts explicitly with Company.
\item Company appears to override our settings in \texttt{company-active-map}
based on \texttt{company-auto-complete-chars}. Turning it off ensures we
have full control.
\item Prevent Company completions from being lowercased in the
completion menu. This has only been observed to happen for
comments and strings in Clojure. (Although in general it will
happen wherever the Dabbrev backend is invoked.)
\item Only search the current buffer to get suggestions for
\texttt{company-dabbrev} (a backend that creates suggestions from text
found in your buffers). This prevents Company from causing lag
once you have a lot of buffers open.
\item Make company-dabbrev case-sensitive. Case insensitivity seems
like a great idea, but it turns out to look really bad when you
have domain-specific words that have particular casing.
\end{itemize}

\begin{minted}[]{common-lisp}
(setq company-idle-delay 0)
(setq company-minimum-prefix-length 3)
(setq company-tooltip-limit 10)
(setq company-tooltip-minimum company-tooltip-limit)
(setq company-show-numbers t)
(setq company-auto-complete-chars nil)
(setq company-dabbrev-downcase nil)
(setq company-dabbrev-other-buffers nil)
(setq company-dabbrev-ignore-case nil)
\end{minted}

\begin{enumerate}
\item Performance
\label{sec:orgb1e57ed}
In case autocompletion is making Emacs drag, we add a toggle to slow
it down.

\begin{minted}[]{common-lisp}

(defun the-company-toggle-slow ()
  "Slow down `company' by turning up the delays before completion starts.
This is done in `the-slow-autocomplete-mode'."
  (if the-slow-autocomplete-mode
      (progn
        (setq-local company-idle-delay 1)
        (setq-local company-minimum-prefix-length 3))
    (kill-local-variable 'company-idle-delay)
    (kill-local-variable 'company-minimum-prefix-length)))

(add-hook 'the-slow-autocomplete-mode-hook #'the-company-toggle-slow)
\end{minted}

\item YaSnippet Hack
\label{sec:orgb70ace6}
Make it so that Company's keymap overrides Yasnippet's keymap when a
snippet is active. This way, you can TAB to complete a suggestion for
the current field in a snippet, and then TAB to move to the next
field. Plus, C-g will dismiss the Company completions menu rather than
cancelling the snippet and moving the cursor while leaving the
completions menu on-screen in the same location.

\begin{minted}[]{common-lisp}
(with-eval-after-load 'yasnippet
  ;; FIXME: this is all a horrible hack, can it be done with
  ;; `bind-key' instead?
  ;;
  ;; This function translates the "event types" I get from
  ;; `map-keymap' into things that I can pass to `lookup-key'
  ;; and `define-key'. It's a hack, and I'd like to find a
  ;; built-in function that accomplishes the same thing while
  ;; taking care of any edge cases I might have missed in this
  ;; ad-hoc solution.
  (defun the-normalize-event (event)
    "This function is a complete hack, do not use.
  But in principle, it translates what we get from `map-keymap'
  into what `lookup-key' and `define-key' want."
    (if (vectorp event)
        event
      (vector event)))

  ;; Here we define a hybrid keymap that delegates first to
  ;; `company-active-map' and then to `yas-keymap'.
  (setq the-yas-company-keymap
        ;; It starts out as a copy of `yas-keymap', and then we
        ;; merge in all of the bindings from
        ;; `company-active-map'.
        (let ((keymap (copy-keymap yas-keymap)))
          (map-keymap
           (lambda (event company-cmd)
             (let* ((event (the-normalize-event event))
                    (yas-cmd (lookup-key yas-keymap event)))
               ;; Here we use an extended menu item with the
               ;; `:filter' option, which allows us to
               ;; dynamically decide which command we want to
               ;; run when a key is pressed.
               (define-key keymap event
                 `(menu-item
                   nil ,company-cmd :filter
                   (lambda (cmd)
                     ;; There doesn't seem to be any obvious
                     ;; function from Company to tell whether or
                     ;; not a completion is in progress (à la
                     ;; `company-explicit-action-p'), so I just
                     ;; check whether or not `company-my-keymap'
                     ;; is defined, which seems to be good
                     ;; enough.
                     (if company-my-keymap
                         ',company-cmd
                       ',yas-cmd))))))
           company-active-map)
          keymap))

  ;; The function `yas--make-control-overlay' uses the current
  ;; value of `yas-keymap' to build the Yasnippet overlay, so to
  ;; override the Yasnippet keymap we only need to dynamically
  ;; rebind `yas-keymap' for the duration of that function.
  (defun the-advice-company-overrides-yasnippet
      (yas--make-control-overlay &rest args)
    "Allow `company' to override `yasnippet'.
  This is an `:around' advice for `yas--make-control-overlay'."
    (let ((yas-keymap the-yas-company-keymap))
      (apply yas--make-control-overlay args)))

  (advice-add #'yas--make-control-overlay :around
              #'the-advice-company-overrides-yasnippet))
\end{minted}
\end{enumerate}

\subsubsection{Company}
\label{sec:org572642e}
\texttt{company} provides an in-buffer autocompletion framework. It
allows for packages to define backends that supply completion
candidates, as well as optional documentation and source code. Then
Company allows for multiple frontends to display the candidates, such
as a tooltip menu. Company stands for "Complete Anything".

\begin{minted}[]{common-lisp}
(defvar the-company-backends-global
  '(;company-capf
    company-files
    (company-dabbrev-code company-keywords)
    company-dabbrev)
  "Values for `company-backends' used everywhere.
If `company-backends' is overridden by The, then these
backends will still be included.")
\end{minted}

\begin{minted}[]{common-lisp}
(use-package company
  :demand t
  :config
  (company-tng-configure-default)
  <<company-config>>
  <<company-slow>>
  (global-company-mode +1)
  :diminish company-mode)

;(use-package posframe
;  :demand t
;  :diminish)

;(use-package company-childframe
;  :demand t
;  :config
;  (company-childframe-mode 1)
;  :diminish)
\end{minted}

\subsubsection{Company Statistics}
\label{sec:orgf50dcb1}
\texttt{company-statistics} adds usage-based sorting to Company completions.
It is a goal to replace this package with \href{https://github.com/PythonNut/historian.el}{\texttt{historian}} or \href{https://github.com/raxod502/prescient.el}{\texttt{prescient}}.

\begin{minted}[]{common-lisp}
(use-package company-statistics
  :demand t
  :config

  ;; Let's future-proof our patching here just in case we ever decide
  ;; to lazy-load company-statistics.
  (el-patch-feature company-statistics)

  ;; Disable the message that is normally printed when
  ;; `company-statistics' loads its statistics file from disk.
  (el-patch-defun company-statistics--load ()
    "Restore statistics."
    (load company-statistics-file 'noerror
          (el-patch-swap nil 'nomessage)
          'nosuffix))

  ;; Enable Company Statistics.
  (company-statistics-mode +1))
\end{minted}
\section{Languages}
\label{sec:org04bf76e}
\subsection{Common Lisp}
\label{sec:orgf9f5207}
\subsubsection{Aggressive Indent}
\label{sec:org040c036}
Enable aggressive indentation in all Lisp modes.
\begin{minted}[]{common-lisp}
(add-hook 'lisp-mode-hook #'aggressive-indent-mode)
\end{minted}
\subsection{Emacs Lisp}
\label{sec:org5678607}
\subsubsection{Hooks}
\label{sec:orgddd0ae9}
Enable ElDoc for Elisp buffers and the \textbf{scratch} buffer.
\begin{minted}[]{common-lisp}
(add-hook 'emacs-lisp-mode-hook #'eldoc-mode)
\end{minted}

Enable Aggressive Indent for Elisp buffers and the \textbf{scratch} buffer.

\begin{minted}[]{common-lisp}
(add-hook 'emacs-lisp-mode-hook #'aggressive-indent-mode)
\end{minted}

\subsubsection{Fixes}
\label{sec:orgb610670}
\begin{enumerate}
\item Advised Function Noise
\label{sec:org42534b2}
Emacs barfs up a bunch of nonsense warnings every time a function is
advised, and we do that a lot, so we'll just tell it to hush.

\begin{minted}[]{common-lisp}
(setq ad-redefinition-action 'accept)
\end{minted}
\end{enumerate}

\subsubsection{Reloading the Init File}
\label{sec:org3dda503}
First, we define a customizable keybinding to reload our init file.

\begin{minted}[]{common-lisp}
(defcustom the-reload-init-keybinding
  (the-join-keys the-prefix "r")
  "The keybinding for reloading init.el, as a string.
Nil means no keybinding is established."
  :group 'the
  :type 'string)
\end{minted}

Now we define a function to actually do the reload and bind it to our
key.

\begin{minted}[]{common-lisp}
(defun the-reload-init ()
  "Reload init.el."
  (interactive)
  (straight-transaction
    (straight-mark-transaction-as-init)
    (message "Reloading init.el...")
    (load user-init-file nil 'nomessage)
    (message "Reloading init.el... done.")))

(bind-key the-reload-init-keybinding #'the-reload-init)
\end{minted}

\subsubsection{Evaluate an Elisp buffer}
\label{sec:org5d14316}
Other Lisp interaction modes (like CIDER and Geiser) provide a binding
for evaluating a whole buffer. We add a similar binding for
\texttt{eval-buffer}, as well as some sanity-checking so we don't evaluate
the init file in a bad way.

\begin{minted}[]{common-lisp}
(defun the-eval-buffer ()
  "Evaluate the current buffer as Elisp code."
  (interactive)
  (message "Evaluating %s..." (buffer-name))
  (straight-transaction
    (if (null buffer-file-name)
        (eval-buffer)
      (when (string= buffer-file-name user-init-file)
        (straight-mark-transaction-as-init))
      (load buffer-file-name nil 'nomessage)))
  (message "Evaluating %s... done." (buffer-name)))

(bind-key "C-c C-k" #'the-eval-buffer emacs-lisp-mode-map)
\end{minted}

\subsubsection{Rebind Find Commands}
\label{sec:org9c202ea}
Add keybindings (\texttt{C-h C-f} and \texttt{C-h C-v}) for jumping to the source of
Elisp functions and variables. Also, add a keybinding (\texttt{C-h C-o}) that
performs the functionality of \texttt{M-.} only for Elisp, because the latter
command is often rebound by other major modes. Note that this
overrides the default bindings of \texttt{C-h C-f} (\texttt{view-emacs-FAQ}) and
\texttt{C-h C-o} (\texttt{describe-distribution}), but I've never used those in 10
years of Emacsing.

\begin{minted}[]{common-lisp}
(defun find-symbol (&optional symbol)
  "Same as `xref-find-definitions' but only for Elisp symbols."
  (interactive)
  (let ((xref-backend-functions '(elisp--xref-backend)))
    (if symbol
        (xref-find-definitions symbol)
      (call-interactively 'xref-find-definitions))))

(bind-keys
 ("C-h C-f" . find-function)
 ("C-h C-v" . find-variable)
 ("C-h C-o" . find-symbol))
\end{minted}

\subsubsection{Lisp Interaction Lighter}
\label{sec:orga5a31c6}
Show `lisp-interaction-mode' as "ξι" instead of "Lisp Interaction" in
the mode line.

\begin{minted}[]{common-lisp}
(defun the--rename-lisp-interaction-mode ()
  (setq mode-name "ξι"))

(add-hook 'lisp-interaction-mode-hook
          #'the--rename-lisp-interaction-mode)
\end{minted}
\section{Performance}
\label{sec:org4552ec4}
\subsection{Performance Mode}
\label{sec:orgde1ece1}
Occasionally features like indentation and autocompletion are
expensive, so we set up a minor mode to slow them down.
\subsubsection{Aggressive Byte Compilation}
\label{sec:orga8e616f}
\begin{minted}[]{common-lisp}
(use-package auto-compile
  :init
  (setq auto-compile-display-buffer nil)
  (setq auto-compile-mode-line-counter t)
  (setq auto-compile-update-autoloads t)
  :config
  (auto-compile-on-load-mode)
  (auto-compile-on-save-mode))
\end{minted}
\subsubsection{Modes}
\label{sec:org74ca9ac}
\begin{minted}[]{common-lisp}
(define-minor-mode the-slow-indent-mode
  "Minor mode for when the indentation code is slow.
This prevents `aggressive-indent' from indenting as frequently.")

(define-minor-mode the-slow-autocomplete-mode
  "Minor mode for when the autocompletion code is slow.
This prevents `company' and `eldoc' from displaying metadata as
quickly.")
\end{minted}
\section{Networking}
\label{sec:org5979838}
\subsection{Network Services}
\label{sec:orgacc991a}
\subsubsection{macOS TLS verification}
\label{sec:org5cbc9b3}
TLS certs on macOS don't live anywhere that \texttt{gnutls} can see them, by
default, so \texttt{brew install libressl} and we'll use those.
\begin{minted}[]{common-lisp}
(the-with-operating-system macOS
  (with-eval-after-load 'gnutls
    (setq gnutls-verify-error t)
    (setq gnutls-min-prime-bits 3072)
    (add-to-list 'gnutls-trustfiles "/usr/local/etc/libressl/cert.pem")))
\end{minted}

\subsubsection{StackOverflow}
\label{sec:org51c5a19}
Honestly, probably the most important package here. \texttt{M-x
sx-authenticate}, provide a username and password, then get to
overflowing.
\begin{minted}[]{common-lisp}
(use-package sx)
\end{minted}

\subsubsection{Bug URL references}
\label{sec:org5bc8837}
Allow setting the regexp for bug references from file-local or
directory-local variables. CIDER does this in its files, for example.
\begin{minted}[]{common-lisp}
(put 'bug-reference-bug-regexp 'safe-local-variable #'stringp)
\end{minted}

\subsubsection{Pastebin}
\label{sec:org8a8f939}
\texttt{ix.io} is a slick little pastebin, and now we can use it in Emacs.
\begin{minted}[]{common-lisp}
(use-package ix)
\end{minted}

\subsubsection{Browsing}
\label{sec:orgaec044f}
\texttt{eww} is the wonderfully named Emacs Web Wowser, a text-based browser.
\begin{minted}[]{common-lisp}
(use-package eww
  :bind* (("M-T g x" . eww)
          ("M-T g :" . eww-browse-with-external-browser)
          ("M-T g #" . eww-list-histories)
          ("M-T g {" . eww-back-url)
          ("M-T g }" . eww-forward-url))
  :config
  (progn
    (add-hook 'eww-mode-hook 'visual-line-mode)))
\end{minted}

\subsubsection{RSS}
\label{sec:org21561d7}
Org does everything, so we manage our RSS feeds with Org, as well.
\begin{minted}[]{common-lisp}
(use-package elfeed
  :demand t)

(use-package elfeed-org
  :demand t
  :config
  (elfeed-org))
\end{minted}
\subsubsection{Steam}
\label{sec:orgea7cac5}
\begin{minted}[]{common-lisp}
(use-package steam
  :init
  (setq steam-username "prooftechnique"))
\end{minted}
\section{Etc.}
\label{sec:org8d403ff}
\subsection{Miscellaneous Utilities}
\label{sec:org2f2f597}
\subsubsection{Alerts}
\label{sec:org3795ca9}
\begin{minted}[]{common-lisp}
(use-package alert
  :demand t
  :config
  (the-with-operating-system macOS
    (setq alert-default-style 'osx-notifier)))
\end{minted}
\subsubsection{Eventually-obsolete Functions}
\label{sec:org89183db}
These functions will become unnecessary in Emacs 26.1, which
extends \texttt{map-put} to have a TESTFN argument.

\begin{minted}[]{common-lisp}
(defun the-alist-set (key val alist &optional symbol)
  "Set property KEY to VAL in ALIST. Return new alist.
This creates the association if it is missing, and otherwise sets
the cdr of the first matching association in the list. It does
not create duplicate associations. By default, key comparison is
done with `equal'. However, if SYMBOL is non-nil, then `eq' is
used instead.
This method may mutate the original alist, but you still need to
use the return value of this method instead of the original
alist, to ensure correct results."
  (if-let* ((pair (if symbol (assq key alist) (assoc key alist))))
      (setcdr pair val)
    (push (cons key val) alist))
  alist)

(defmacro the-alist-set* (key val alist &optional symbol)
  "Set property KEY to VAL in ALIST. Return new alist.
ALIST must be a literal symbol naming a variable holding an
alist. That variable will be re-set using `setq'. By default, key
comparison is done with `equal'. However, if SYMBOL is non-nil,
then `eq' is used instead. See also `the-alist-set'."
  `(setq ,alist (the-alist-set ,key ,val ,alist ,symbol)))

(defun the-insert-after (insert-elt before-elt list &optional testfn)
  "Insert INSERT-ELT after BEFORE-ELT in LIST, returning copy of LIST.
The original LIST is not modified. If BEFORE-ELT is not in LIST,
it is inserted at the end. Element comparison is done with
TESTFN, which defaults to `eq'. See also `the-insert-before'
and `the-insert-after*'."
  (let ((testfn (or testfn #'eq)))
    (cond
     ((null list)
      (list insert-elt))
     ((funcall testfn before-elt (car list))
      (append (list (car list) insert-elt) (copy-sequence (cdr list))))
     (t (cons (car list)
              (the-insert-after
               insert-elt before-elt (cdr list) testfn))))))

(defmacro the-insert-after* (insert-elt before-elt list &optional testfn)
  "Insert INSERT-ELT after BEFORE-ELT in LIST, returning copy of LIST.
LIST must be a literal symbol naming a variable holding a list.
That variable will be re-set using `setq'. Element comparison is
done with TESTFN, which defaults to `eq'. See also
`the-insert-after' and `the-insert-before'."
  `(setq ,list (the-insert-after ,insert-elt ,before-elt ,list ,testfn)))

(defun the-insert-before (insert-elt after-elt lst &optional testfn)
  "Insert INSERT-ELT before AFTER-ELT in LIST, returning copy of LIST.
The original LIST is not modified. If BEFORE-ELT is not in LIST,
it is inserted at the beginning. Element comparison is done with
TESTFN, which defaults to `eq'. See also `the-insert-after'
and `the-insert-before*'."
  (nreverse (the-insert-after insert-elt after-elt (reverse lst) testfn)))

(defmacro the-insert-before* (insert-elt after-elt list &optional testfn)
  "Insert INSERT-ELT before AFTER-ELT in LIST, returning copy of LIST.
LIST must be a literal symbol naming a variable holding a list.
That variable will be re-set using `setq'. Element comparison is
done with TESTFN, which defaults to `eq'. See also
`the-insert-before' and `the-insert-after*'."
  `(setq ,list (the-insert-before ,insert-elt ,after-elt ,list ,testfn)))
\end{minted}

\subsubsection{Framework Identification}
\label{sec:org40c54eb}
This gives us an easy way to check if the file we're editing is part
of THE.

\begin{minted}[]{common-lisp}
(defun the-managed-p (filename)
  "Return non-nil if FILENAME is managed by The.
This means that FILENAME is a symlink whose target is inside
`the-directory'."
  (and the-directory
       (string-prefix-p the-directory (file-truename filename)
                        ;; The filesystem on macOS is case-insensitive
                        ;; but case-preserving, so we have to compare
                        ;; case-insensitively in that situation.
                        (eq the-operating-system 'macOS))))
\end{minted}
\end{document}
